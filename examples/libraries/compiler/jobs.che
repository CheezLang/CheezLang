
use import std.fiber
use import std.string
use import std.array
use import std.mem.std_heap_allocator

fs  :: import std.io.fs
io  :: import std.io
fmt :: import std.fmt

use import logging.logger

use import ast
use import lexer
use import parser
use import ast_dumper
use import compiler
use import bytecode_generator
use import scope
use import config
use import value


#export_scope

CompilationJob :: trait {
    progressed  : bool = false
    done        : bool = false
    compiler    : &Compiler = null

    get_name :: (ref Self) -> string;
    execute :: (ref Self, compiler: ref Compiler);
}

LoadFileJob :: struct CompilationJob {
    name : String
    path : String
}

impl LoadFileJob {
    new :: (path: String) -> LoadFileJob {
        return LoadFileJob(
            name = fmt.format("LoadFileJob '{}'", [path])
            path = path
        )
    }
}

CompileGlobalNodeJob :: struct CompilationJob {
    name : String
    node : &AstNode
}

impl CompileGlobalNodeJob {
    new :: (node: &AstNode) -> CompileGlobalNodeJob {
        return CompileGlobalNodeJob(
            name = fmt.format("CompileGlobalNodeJob #{}", [node.id])
            node = node
        )
    }
}

impl CompilationJob for CompileGlobalNodeJob {
    get_name :: (ref Self) -> string {
        return name.slice()
    }

    debug_log :: (ref Self, msg: string, args: []&any = []) {
        if const DEBUG_JOBS {
            str := fmt.format(msg, args)
            g_logger.log("[{}] [CompileGlobalNode #{}] {}", [Fiber.user_data(FiberContext).thread, node.id, str])
        } 
    }

    execute :: (ref Self, compiler: ref Compiler) {
        debug_log("Begin compilation")

        node.scope = compiler.global_scope
        compile_node(<<node)

        debug_log("Done")
    }

    compile_node :: (ref Self, node: ref AstNode) {
        match node {
            AstConstDecl($decl) -> compile_const_decl(decl)
            AstFunction($func)  -> compile_function(func)
            AstBlock($block)    -> compile_block(block)
            AstCall($call)      -> compile_call(call)
            AstIdentifier($id)  -> compile_identifier(id)

            _ -> {
                dump := dump_ast(node, recurse = false)
                compiler.error_handler.report_error_at(node.location, "compile_node: not implemented: {}", [dump])
            }
        }
    }

    compile_identifier :: (ref Self, id: ref AstIdentifier) {
        sym := id.scope.get_symbol(id.name)
        match sym {
            .Val(($typ, $value)) -> match value {
                .Function($func) -> {
                    // id.typ = cast typ
                    id.value = value
                }
                
                $val -> {
                    compiler.error_handler.report_error_at(id.location, "[compile_identifier] not implemented: {}", [val])
                }
            }

            .ConstDecl($decl) -> {
                wait_until(decl.value_expr.value != .None)
                id.value = decl.value_expr.value
            }

            $sym -> {
                compiler.error_handler.report_error_at(id.location, "[compile_identifier] not implemented: {}", [sym])
            }
        }
    }

    compile_call :: (ref Self, call: ref AstCall) {
        call.function.scope = call.scope
        compile_node(call.function)
        
        match call.function.value {
            .Function($func) -> {
                // call.typ = cast typ
                // call.value = value
                call.ast_function = func
            }
            
            $val -> {
                compiler.error_handler.report_error_at(call.location, "[compile_call] not implemented: {}", [val])
            }
        }
    }

    compile_block :: (ref Self, block: ref AstBlock) {
        block.sub_scope = compiler.new_scope(block.scope)

        for child : block.children {
            child.scope = block.sub_scope
            compile_node(<<child)
        }
    }

    compile_function :: (ref Self, func: ref AstFunction) {
        debug_log("compile_function '{}': Begin", [func.name])
        func.param_scope = compiler.new_scope(func.scope)
        func.body.scope = func.param_scope
        func.value = Value.Function(&func)

        debug_log("compile_function '{}': Compile Body", [func.name])
        compile_node(<<func.body)

        debug_log("compile_function '{}': Generate Bytecode", [func.name])
        code_gen := ByteCodeGenerator.new(compiler.string_database, <<compiler.error_handler, compiler.bc_allocator)
        code_gen.get().generate_code_for_function(&func)

        // @temp
        if func.name == "Main" {
            debug_log("compile_function '{}': Call Main", [func.name])
            context := Fiber.user_data(FiberContext)
            context.interp.comp_call(func)
            compiler.code_runner.get().run(<<context.interp)
        }
        debug_log("compile_function '{}': End", [func.name])
    }

    compile_const_decl :: (ref Self, decl: ref AstConstDecl) {
        decl.value_expr.scope = decl.scope

        match decl.value_expr {
            AstFunction($func) -> {
                func.name = decl.name.name
            }
        }

        match decl.scope.define_const_decl(decl.name.name, decl) {
            Err() -> compiler.error_handler.report_error_at(decl.name.location, "A symbol with this name already exists in this scope")
        }

        compile_node(decl.value_expr)
        decl.value = decl.value_expr.value
    }
}

impl CompilationJob for LoadFileJob {
    get_name :: (ref Self) -> string {
        return name.slice()
    }

    debug_log :: (ref Self, msg: string, args: []&any = []) {
        if const DEBUG_JOBS {
            str := fmt.format(msg, args)
            g_logger.log("[{}] [LoadFileJob '{}'] {}", [Fiber.user_data(FiberContext).thread, path, str])
        } 
    }

    execute :: (ref Self, compiler: ref Compiler) {
        debug_log("Begin loading")

        full_path := match fs.get_full_path(path.slice()) {
            Ok($p) -> compiler.string_database.intern(p.slice())
            Err(_) -> compiler.string_database.intern(path.slice())
        }

        if !compiler.sources.contains(full_path) {
            content := fs.read_file(full_path)

            match content {
                Ok($content) -> {
                    lexer := Lexer.from_string(content.slice(), compiler.string_database)
                    lexer.get().location.file = full_path
                    parser := Parser.new(lexer.get(), compiler.error_handler, compiler.ast_allocator)

                    loop {
                        decl := parser.parse_constant_declaration()
                        if decl == null then break
                        dump := dump_ast(<<decl)
                        // io.println(dump)

                        compiler.add_job(CompileGlobalNodeJob.new(decl))
                    }

                    compiler.sources.set(full_path, content)
                }

                Err(_) -> {
                    compiler.error_handler.report_error("Failed to load source file '{}'", [full_path])
                }
            }
        }
        
        debug_log("Done")
    }
}


#file_scope
