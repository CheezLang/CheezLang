 use import std.array
use import std.string

fmt :: import std.fmt

use import logging.logger

use import ast
use import ast_dumper

#export_scope

pretty_print_ast :: (node: &AstNode) -> String {
    result := String.empty()
    pretty_print_ast_helper(&result, node)
    return result
}

pretty_print_typed_ast :: (node: &AstNode) -> String {
    result := String.empty()
    pretty_print_typed_ast_helper(&result, node)
    return result
}

#file_scope

append_indented :: (result: &String, str: &String, indent: int) {
    for line in str.slice().split_at_char('`n') {
        for 0..indent {
            result += " "
        }
        result += line
        result += "`n"
    }
}

pretty_print_ast_helper :: (result: &String, node: &AstNode) {
    match node {
        AstStruct($node) -> {
            fmt.format_into(result, "struct {} ``{`n", [node.name])

            r := String.empty()
            for child in &node.children {
                r.clear()
                pretty_print_ast_helper(&r, &**child)
                append_indented(result, &r, 4)
            }

            fmt.format_into(result, "``}")
        }
        
        AstTrait($node) -> {
            fmt.format_into(result, "trait {} ``{`n", [node.name])

            r := String.empty()
            for child in &node.children {
                r.clear()
                pretty_print_ast_helper(&r, &**child)
                append_indented(result, &r, 4)
            }

            fmt.format_into(result, "``}")
        }
        
        AstEnum($node) -> {
            fmt.format_into(result, "enum {} ``{`n", [node.name])

            r := String.empty()
            for child in &node.children {
                r.clear()
                pretty_print_ast_helper(&r, &**child)
                append_indented(result, &r, 4)
            }

            fmt.format_into(result, "``}")
        }

        AstImpl($imp) -> {
            result += "impl "

            if imp.trait_expr != null {
                pretty_print_ast_helper(result, &*imp.trait_expr)
                result += " for "
            }
            pretty_print_ast_helper(result, imp.target_expr)

            result += " {`n"

            r := String.empty()
            for child in &imp.children {
                r.clear()
                pretty_print_ast_helper(&r, &**child)
                append_indented(result, &r, 4)
            }

            fmt.format_into(result, "``}")
        }

        AstFunction($func) -> {
            result += "("
            for param in &func.params {
                if it_index > 0 then result.append_string(", ")
                pretty_print_ast_helper(result, &**param)
            }
            result += ") "
            if func.return_type_expr != null {
                pretty_print_ast_helper(result, &*func.return_type_expr)
            }
            if func.body != null {
                pretty_print_ast_helper(result, &*func.body)
            }
        }

        AstTuple($tuple) -> {
            result += "("
            for value in &tuple.values {
                if it_index > 0 then result.append_string(", ")
                pretty_print_ast_helper(result, &**value)
            }
            result += ")"
        }

        AstConstDecl($decl) -> {
            pretty_print_ast_helper(result, decl.pattern)
            result += " :"
            if decl.type_expr != null then {
                result += " "
                pretty_print_ast_helper(result, &*decl.type_expr)
                result += " "
            }
            result += ": "
            pretty_print_ast_helper(result, decl.value_expr)
        }

        AstDecl($decl) -> {
            if decl.mutable {
                result += "mut "
            }
            if decl.pattern != null {
                pretty_print_ast_helper(result, &*decl.pattern)
            } else {
                result += "_"
            }
            result += " :"
            if decl.type_expr != null then {
                result += " "
                pretty_print_ast_helper(result, &*decl.type_expr)

                if decl.value_expr != null {
                    result += " "
                }
            }
            if decl.value_expr != null {
                result += "= "
                pretty_print_ast_helper(result, &*decl.value_expr)
            }
        }

        AstAssignment($ass) -> {
            pretty_print_ast_helper(result, ass.pattern)
            result += " = "
            pretty_print_ast_helper(result, ass.value_expr)
        }

        AstBlock($block) -> {
            r := String.empty()

            result += "{`n"
            for child in &block.children {
                r.clear()
                pretty_print_ast_helper(&r, &**child)
                append_indented(result, &r, 4)
            }
            result += "}"
        }

        AstIdentifier($id) -> {
            result += id.name
        }

        AstNumberLiteral($num) -> {
            fmt.format_into(result, "{}", [num.value])
        }

        AstString($str) -> {
            fmt.format_into(result, "{}", [str.string_value])
        }

        AstBool($bol) -> {
            fmt.format_into(result, "{}", [bol.bool_value])
        }

        AstNull($nul) -> {
            result += "null"
        }

        AstBinary($bin) -> {
            result += "("
            pretty_print_ast_helper(result, bin.left)
            fmt.format_into(result, " {} ", [bin.operator.lexeme()])
            pretty_print_ast_helper(result, bin.right)
            result += ")"
        }

        AstUfc($ufc) -> {
            pretty_print_ast_helper(result, ufc.value_expr)
        }

        AstUse($us) -> {
            result += "use "
            pretty_print_ast_helper(result, us.value_expr)
        }

        AstUnary($un) -> {
            result += "("
            fmt.format_into(result, "{}", [un.operator.lexeme()])
            pretty_print_ast_helper(result, un.sub)
            result += ")"
        }

        AstArgument($arg) -> {
            if arg.name != null {
                pretty_print_ast_helper(result, &*arg.name)
                result += " = "
            }
            pretty_print_ast_helper(result, arg.value_expr)
        }

        AstCall($call) -> {
            pretty_print_ast_helper(result, call.function)
            result += "("
            for arg in &call.arguments {
                if it_index > 0 {
                    result += ", "
                }
                pretty_print_ast_helper(result, &**arg)
            }
            result += ")"
        }

        AstCast($cst) -> {
            result += "cast"
            if cst.type_expr != null {
                result += "("
                pretty_print_ast_helper(result, &*cst.type_expr)
                result += ")"
            }
            result += " "
            pretty_print_ast_helper(result, cst.value_expr)
        }

        AstIf($iff) -> {
            result += "if "
            pretty_print_ast_helper(result, iff.condition)
            result += " then "
            pretty_print_ast_helper(result, iff.true_case)

            if iff.false_case != null {
                result += " else "
                pretty_print_ast_helper(result, &*iff.false_case)
            }
        }

        AstIndex($node) -> {
            pretty_print_ast_helper(result, node.sub)
            result += "["
            for arg in &node.arguments {
                if it_index > 0 {
                    result += ", "
                }
                pretty_print_ast_helper(result, &**arg)
            }
            result += "]"
        }

        AstLoop($lop) -> {
            result += "loop "
            pretty_print_ast_helper(result, lop.body)
        }

        AstFor($forr) -> {
            result += "for "
            if forr.it_pattern != null then
                pretty_print_ast_helper(result, &*forr.it_pattern)
            if forr.index_name != null {
                if forr.it_pattern != null {
                    result += ", "
                }
                pretty_print_ast_helper(result, &*forr.index_name)
            }

            if forr.it_pattern != null or forr.index_name != null {
                result += " in "
            }
            pretty_print_ast_helper(result, forr.collection)
            result += " do "
            pretty_print_ast_helper(result, forr.body)
        }

        AstBreak($brake) -> {
            result += "break"
            if brake.label != null {
                result += " "
                pretty_print_ast_helper(result, &*brake.label)
            }
            if brake.value_expr != null {
                result += " with "
                pretty_print_ast_helper(result, &*brake.value_expr)
            }
        }

        AstContinue($cont) -> {
            result += "continue"
            if cont.label != null {
                result += " "
                pretty_print_ast_helper(result, &*cont.label)
            }
        }

        AstReturn($ret) -> {
            result += "return"
            if ret.value_expr != null {
                result += " "
                pretty_print_ast_helper(result, &*ret.value_expr)
            }
        }

        AstDefer($def) -> {
            result += "deref "
            pretty_print_ast_helper(result, def.sub)
        }

        AstImport($imp) -> {
            result += "import "
            pretty_print_ast_helper(result, imp.path)
        }

        AstMatch($mat) -> {
            result += "match "
            if mat.value_expr != null then
                pretty_print_ast_helper(result, &*mat.value_expr)
            result += " {`n"
            r := String.empty()
            for case in &mat.cases {
                r.clear()
                pretty_print_ast_helper(&r, case.pattern)

                if case.condition != null {
                    &r += " if"
                    pretty_print_ast_helper(&r, &*case.condition)
                }

                &r += " -> "
                pretty_print_ast_helper(&r, case.body)
                append_indented(result, &r, 4)
            }
            result += "}"
        }

        AstPoly($poly) -> {
            result += "("
            for param in &poly.params {
                if it_index > 0 {
                    result += ", "
                }
                pretty_print_ast_helper(result, &**param)
            }
            result += ") "
            match poly.value_expr {
                AstEnum(_) -> {}
                AstStruct(_) -> {}
                AstTrait(_) -> {}
                _ -> {
                    result += "const "
                }
            }
            pretty_print_ast_helper(result, poly.value_expr)
        }

        AstTuple($tuple) -> {
            result += "("
            for v in &tuple.values {
                pretty_print_ast_helper(result, &**v)
            }
            result += ")"
        }

        AstArray($array) -> {
            result += "["
            for v in &array.values {
                if it_index > 0 {
                    result += ", "
                }
                pretty_print_ast_helper(result, &**v)
            }
            result += "]"
        }

        AstArrayType($array_type) -> {
            result += "["
            if array_type.count != null {
                pretty_print_ast_helper(result, &*array_type.count)
            }
            result += "]"
            pretty_print_ast_helper(result, array_type.target)
        }

        AstDot($dot) -> {
            if dot.sub != null then
                pretty_print_ast_helper(result, &*dot.sub)
            result += "."
            pretty_print_ast_helper(result, dot.name)
        }

        $node -> {
            dump := dump_ast(node)
            g_logger.log("[ERROR] pretty_print_ast: node type not implemented: {}`n", [dump])
        }
    }
}

pretty_print_typed_ast_helper :: (result: &String, node: &AstNode) {
    match node {
        AstStruct($str) -> {
            fmt.format_into(result, "struct {} ``{ // size: {}, alignment: {}`n", [str.name, str.struct_type.size, str.struct_type.alignment])
            // @todo

            r := String.empty()
            for mem in &str.members {
                r.clear()
                pretty_print_typed_ast_helper(&r, &**mem)
                append_indented(result, &r, 4)
            }
            fmt.format_into(result, "``}")
        }
        
        AstTrait($str) -> {
            fmt.format_into(result, "trait ``{")
            // @todo
            fmt.format_into(result, "``}")
        }
        
        AstEnum($str) -> {
            fmt.format_into(result, "enum ``{")
            // @todo
            fmt.format_into(result, "``}")
        }

        AstFunction($func) -> {
            result += "("
            for param in &func.params {
                if it_index > 0 then result.append_string(", ")
                pretty_print_typed_ast_helper(result, &**param)
            }
            result += ") "
            if func.return_type != null {
                fmt.format_into(result, "-> {} ", [func.return_type])
            }
            pretty_print_typed_ast_helper(result, &*func.body)
        }

        AstTuple($tuple) -> {
            result += "("
            for value in &tuple.values {
                if it_index > 0 then result.append_string(", ")
                pretty_print_typed_ast_helper(result, &**value)
            }
            result += ")"
        }

        AstConstDecl($decl) -> {
            // g_logger.log("pretty_print_typed_ast_helper AstConstDecl")
            pretty_print_typed_ast_helper(result, decl.pattern)

            fmt.format_into(result, " : {} : ", [decl.var_type])
            match decl.value {
                .Int($i)    -> fmt.format_into(result, "{}", [i])
                .Float($f)  -> fmt.format_into(result, "{}", [f])
                .Bool($b)   -> fmt.format_into(result, "{}", [b])
                .String($s) -> fmt.format_into(result, "{}", [s])
                _ -> pretty_print_typed_ast_helper(result, decl.value_expr)
            }
        }

        AstDecl($decl) -> {
            if decl.mutable {
                result += "mut "
            }
            if decl.pattern != null {
                pretty_print_typed_ast_helper(result, &*decl.pattern)
            } else {
                result += "_"
            }
            fmt.format_into(result, " : {}", [decl.var_type])
            if decl.value_expr != null {
                result += " = "
                pretty_print_typed_ast_helper(result, &*decl.value_expr)
            }
        }

        AstAssignment($ass) -> {
            pretty_print_typed_ast_helper(result, ass.pattern)
            result += " = "
            pretty_print_typed_ast_helper(result, ass.value_expr)
        }

        AstBlock($block) -> {
            r := String.empty()

            result += "{`n"
            for child in &block.children {
                r.clear()
                pretty_print_typed_ast_helper(&r, &**child)
                append_indented(result, &r, 4)
            }
            result += "}"
        }

        AstIdentifier($id) -> {
            result += id.name
        }

        AstNumberLiteral($num) -> {
            fmt.format_into(result, "{}", [num.value])
        }

        AstString($str) -> {
            fmt.format_into(result, "{}", [str.string_value])
        }

        AstBool($bol) -> {
            fmt.format_into(result, "{}", [bol.bool_value])
        }

        AstNull($nul) -> {
            result += "null"
        }

        AstBinary($bin) -> {
            result += "("
            pretty_print_typed_ast_helper(result, bin.left)
            fmt.format_into(result, " {} ", [bin.operator.lexeme()])
            pretty_print_typed_ast_helper(result, bin.right)
            result += ")"
        }

        AstUfc($ufc) -> {
            fmt.format_into(result, "{}", [ufc.symbol])
            // pretty_print_typed_ast_helper(result, ufc.symbol)
        }

        AstUse($us) -> {
            result += "use "
            pretty_print_typed_ast_helper(result, us.value_expr)
        }

        AstUnary($un) -> {
            result += "("
            fmt.format_into(result, "{}", [un.operator.lexeme()])
            pretty_print_typed_ast_helper(result, un.sub)
            result += ")"
        }

        AstArgument($arg) -> {
            if arg.name != null {
                pretty_print_typed_ast_helper(result, &*arg.name)
                result += " = "
            }
            pretty_print_typed_ast_helper(result, arg.value_expr)
        }

        AstCall($call) -> {
            pretty_print_typed_ast_helper(result, call.function)
            result += "("
            for arg in &call.arguments {
                if it_index > 0 {
                    result += ", "
                }
                pretty_print_typed_ast_helper(result, &**arg)
            }
            result += ")"
        }

        AstCast($cst) -> {
            fmt.format_into(result, "cast({}) ", [cst.typ])
            pretty_print_typed_ast_helper(result, cst.value_expr)
        }

        AstIf($iff) -> {
            result += "if "
            pretty_print_typed_ast_helper(result, iff.condition)
            result += " then "
            pretty_print_typed_ast_helper(result, iff.true_case)

            if iff.false_case != null {
                result += " else "
                pretty_print_typed_ast_helper(result, &*iff.false_case)
            }
        }

        AstIndex($node) -> {
            pretty_print_typed_ast_helper(result, node.sub)
            result += "["
            for arg in &node.arguments {
                if it_index > 0 {
                    result += ", "
                }
                pretty_print_typed_ast_helper(result, &**arg)
            }
            result += "]"
        }

        AstLoop($lop) -> {
            result += "loop "
            pretty_print_typed_ast_helper(result, lop.body)
        }

        AstFor($forr) -> {
            result += "for "
            if forr.it_pattern != null then
                pretty_print_typed_ast_helper(result, &*forr.it_pattern)
            if forr.index_name != null {
                if forr.it_pattern != null {
                    result += ", "
                }
                pretty_print_typed_ast_helper(result, &*forr.index_name)
            }

            if forr.it_pattern != null or forr.index_name != null {
                result += " in "
            }
            pretty_print_typed_ast_helper(result, forr.collection)
            result += " do "
            pretty_print_typed_ast_helper(result, forr.body)
        }

        AstBreak($brake) -> {
            result += "break"
            if brake.label != null {
                result += " "
                pretty_print_typed_ast_helper(result, &*brake.label)
            }
            if brake.value_expr != null {
                result += " with "
                pretty_print_typed_ast_helper(result, &*brake.value_expr)
            }
        }

        AstContinue($cont) -> {
            result += "continue"
            if cont.label != null {
                result += " "
                pretty_print_typed_ast_helper(result, &*cont.label)
            }
        }

        AstReturn($ret) -> {
            result += "return"
            if ret.value_expr != null {
                result += " "
                pretty_print_typed_ast_helper(result, &*ret.value_expr)
            }
        }

        AstDefer($def) -> {
            result += "deref "
            pretty_print_typed_ast_helper(result, def.sub)
        }

        AstImport($imp) -> {
            // g_logger.log("pretty_print_typed_ast_helper AstImport")
            result += "import "
            pretty_print_typed_ast_helper(result, imp.path)
        }

        AstMatch($mat) -> {
            result += "match "
            if mat.value_expr != null then
                pretty_print_typed_ast_helper(result, &*mat.value_expr)
            result += " {`n"

            r := String.empty()
            for case in &mat.cases {
                r.clear()
                pretty_print_typed_ast_helper(&r, case.pattern)

                if case.condition != null {
                    &r += " if"
                    pretty_print_typed_ast_helper(&r, &*case.condition)
                }

                &r += " -> "
                pretty_print_typed_ast_helper(&r, case.body)
                append_indented(result, &r, 4)
            }
            result += "}"
        }

        AstPoly($poly) -> {
            result += "("
            for param in &poly.params {
                if it_index > 0 {
                    result += ", "
                }
                pretty_print_typed_ast_helper(result, &**param)
            }
            result += ") "
            match poly.value_expr {
                AstEnum(_) -> {}
                AstStruct(_) -> {}
                AstTrait(_) -> {}
                _ -> {
                    result += "const "
                }
            }
            pretty_print_ast_helper(result, poly.value_expr)
        }

        AstTuple($tuple) -> {
            result += "("
            for v in &tuple.values {
                pretty_print_typed_ast_helper(result, &**v)
            }
            result += ")"
        }

        AstArray($array) -> {
            result += "["
            for v in &array.values {
                if it_index > 0 {
                    result += ", "
                }
                pretty_print_typed_ast_helper(result, &**v)
            }
            result += "]"
        }

        AstArrayType($array_type) -> {
            fmt.format_into(result, "{}", [array_type.value.Type])
        }

        AstDot($dot) -> {
            if dot.sub != null then
                pretty_print_typed_ast_helper(result, &*dot.sub)
            result += "."
            pretty_print_typed_ast_helper(result, dot.name)
        }

        $node -> {
            dump := dump_ast(node)
            g_logger.log("[ERROR] pretty_print_typed_ast: node type not implemented: {}`n", [dump])
        }
    }
}
