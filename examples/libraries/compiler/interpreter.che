use import std.array
use import std.hash_table
use import std.map
use import std.string
use import std.math

io  :: import std.io
fs  :: import std.io.fs
fmt :: import std.fmt
mem :: import std.mem.allocator

use import logging.logger

use import string_database
use import bytecode_generator
use import error_handler
use import ast
use import compiler
use import config
use import value

#export_scope


CodeRunner :: trait {
    run :: (&Self, interp: &Interpreter);
}

DefaultCodeRunner :: struct CodeRunner {

}

impl CodeRunner for DefaultCodeRunner {
    run :: (&Self, interp: &Interpreter) {
        interp.run()
    }
}

LineInfo :: struct #copy {
    line  : int
    count : int
}

VariableInfo :: struct #copy {
    id      : int
    name    : string
    index   : int
}

CBCDebugInfo :: struct {
    path        := String.empty()
    lines       := Array[LineInfo].create()
    locals      := Array[VariableInfo].create()
    parameters  := Array[String].create()
}

impl CBCDebugInfo {
    get_line_for_ip :: (&Self, ip: int) -> int {
        // g_logger.log("get_line_for_ip({}, lines: {})", [ip, lines.slice()])
        i := 0
        for lines {
            if it.count == 0 then continue
            i += it.count
            if i > ip {
                return it.line
            }
        }

        return -1
    }
    get_line_for_ip2 :: (&Self, ip: int, print: Fn(string, []^any, string)) -> int {
        line := get_line_for_ip(ip)
        // print("get_line_for_ip({}) -> {}  (lines: {})", [ip, line, lines.slice()], "console")
        return line
    }
}

CBCFunction :: struct {
    ast_function    : ^AstFunction = null
    instructions    := Array[u64].create()
    local_slots     := 0
    debug_info      := CBCDebugInfo()
}

StackFrame :: struct {
    ip_prev : int   = default
    ip      : int   = default
    sp      : int   = default
    bp      : int   = default
    code    : []u64 = default
    stack   : []int = default
    func    : ^CBCFunction = null
}

impl StackFrame {
    push_int :: (&Self, val: int) {
        stack[sp] = val
        sp += 1
    }

    pop_int :: (&Self) -> int {
        @assert(sp > 0)
        sp -= 1
        return stack[sp]
    }

    get_arg :: (&Self, index: int) -> int {
        @assert(index >= 0 and index < bp)
        return stack[index]
    }

    dump_stack :: (&Self) -> String {
        str := String.with_capacity(128)

        str += "[ "

        // parameters
        for i : 0..bp {
            if i > 0 {
                str += ", "
            }
            str.appendf("{}", stack[i])
        }

        // locals
        str += " | "

        if sp >= bp {
            for i : max(bp, sp - 5) .. sp {
                str.appendf("{}", stack[i])
                if i < sp - 1 {
                    str += ", "
                }
            }
        }

        str += " ]"

        return str
    }
}

Interpreter :: struct {
    sp              : int
    fp              : int
    frames          : [1024 * 8]StackFrame
    stack           : [1024 * 1024]int
    strings         : StringDatabase
    error_handler   : ^ErrorHandler
}

impl Interpreter {
    new :: (error_handler: ^ErrorHandler) -> ^Interpreter {
        interp := mem.alloc(Interpreter)
        <<interp = Interpreter(
            sp          = 0
            fp          = -1
            frames      = default
            stack       = default
            strings     = StringDatabase.new()
            error_handler = error_handler
        )
        return interp
    }

    print :: (format: string, args: []^any = [], category: string = "console") {
        g_logger.log(format, args)
    }

    top_frame :: (&Self) -> ^StackFrame {
        @assert(fp >= 0)
        return ^frames[fp]
    }

    get_stackframes :: (&Self) -> []StackFrame {
        return frames[..=fp]
    }

    comp_call :: (&Self, function: &AstFunction) {
        push_frame(function.bytecode, function.params.count())
    }

    push_frame :: (&Self, func: ^CBCFunction, arg_count: int = 0) {
        if fp + 1 >= frames.length {
            frame := &frames[fp]
            g_logger.log("[INTERP] {}:{}:{}: StackOverflow at (fp={}, sp={}, ip={}) {}           {}", [
                frame.func.debug_info.path.slice()
                frame.func.debug_info.get_line_for_ip(frame.ip_prev)
                5 // @todo: this is the column, read from debug info
                fp
                frame.sp
                frame.ip_prev
                Instruction.pretty_print(frame.code[frame.ip_prev..])
                frame.dump_stack()
            ], "stdout")
            return
        }

        _fp, _sp := fp, sp

        if fp >= 0 {
            sp += frames[fp].sp - frames[fp].bp
        }
        fp += 1

        if sp - arg_count < 0 {
            g_logger.log("[INTERP] Trying to call function with {} arguments but not enough arguments are on the stack (sp = {})", [arg_count, sp])
            // frame := &frames[fp]
            // g_logger.log("[INTERP] {}:{}:{}: Trying to call function with {} arguments but not enough arguments are on the stack at (fp={}, sp={}, ip={}) {}           {}", [
            //     arg_count
            //     frame.func.debug_info.path.slice()
            //     frame.func.debug_info.get_line_for_ip(frame.ip_prev)
            //     5 // @todo: this is the column, read from debug info
            //     fp
            //     frame.sp
            //     frame.ip_prev
            //     Instruction.pretty_print(frame.code[frame.ip_prev..])
            //     frame.dump_stack()
            // ], "stdout")
            return
        }

        frames[fp] = StackFrame(
            ip = 0
            sp = arg_count + func.local_slots
            bp = arg_count
            code = func.instructions.slice()
            stack = stack[sp-arg_count .. ]
            func = func
        )

        if const DEBUG_INTERPRETER {
            g_logger.log("push_frame {} (fp = {} -> {}, sp = {} -> {}, local_slots = {})", [arg_count, _fp, fp, _sp, sp, func.local_slots])
        }
    }

    push_native_frame :: (&Self, arg_count: int) {
        if fp + 1 >= frames.length {
            frame := &frames[fp]
            g_logger.log("[INTERP] {}:{}:{}: StackOverflow at (fp={}, sp={}, ip={}) {}           {}", [
                frame.func.debug_info.path.slice()
                frame.func.debug_info.get_line_for_ip(frame.ip_prev)
                5 // @todo: this is the column, read from debug info
                fp
                frame.sp
                frame.ip_prev
                Instruction.pretty_print(frame.code[frame.ip_prev..])
                frame.dump_stack()
            ], "stdout")
            return
        }

        _fp, _sp := fp, sp
        defer { if const DEBUG_INTERPRETER then g_logger.log("push_native_frame {} (fp = {} -> {}, sp = {} -> {})", [arg_count, _fp, fp, _sp, sp]) }

        if fp >= 0 {
            sp += frames[fp].sp - frames[fp].bp
        }
        fp += 1
        frames[fp] = StackFrame(
            ip = 0
            sp = arg_count
            bp = arg_count
            code = null
            stack = stack[sp-arg_count .. ]
            func = null
        )
    }

    pop_frame :: (&Self) {
        _fp, _sp := fp, sp
        defer { if const DEBUG_INTERPRETER then g_logger.log("pop_frame (fp = {} -> {}, sp = {} -> {})", [_fp, fp, _sp, sp]) }

        fp -= 1
        if fp >= 0 {
            sp -= frames[fp].sp - frames[fp].bp
            (^frames[fp]).sp -= frames[fp + 1].bp
        }
    }

    step :: (&Self) -> enum { Continue, Done } {
        @assert(fp >= 0)
        frame := ^frames[fp]
        arg :: (T: type) #macro {
            val := <<cast(^T)^frame.code[frame.ip]
            frame.ip += 1
            val
        }

        push :: (val: $T) #macro {
            frame.stack[frame.sp] = <<cast(^int)^val
            frame.sp += 1
        }

        pop :: (T: type) #macro {
            @assert(frame.sp > 0)
            frame.sp -= 1
            <<cast(^T)^frame.stack[frame.sp]
        }

        peek :: (T: type, offset: int) #macro {
            @assert(frame.sp > 0)
            @assert(offset <= 0)
            <<cast(^T)^frame.stack[frame.sp + offset - 1]
        }

        set :: ($T: type, offset: int, val: T) #macro {
            @assert(frame.sp > 0)
            @assert(offset <= 0)
            <<cast(^T)^frame.stack[frame.sp + offset - 1] = val
        }


        // fetch
        frame.ip_prev = frame.ip
        opcode := frame.code[frame.ip]

        if const DEBUG_INTERPRETER {
            g_logger.log("{}{}{}{}{}{}{}{}{}{}{}", [
                frame.func.debug_info.get_line_for_ip(frame.ip)
                fmt.align(6), fp
                fmt.align(11), frame.sp
                fmt.align(17), frame.ip
                fmt.align(25), Instruction.pretty_print(frame.code[frame.ip..])
                fmt.align(45), frame.dump_stack()
            ], "stdout")

            //     frame.func.debug_info.get_line_for_ip(frame.ip)
            //     fmt.align(6), fp
            //     fmt.align(11), frame.sp
            //     fmt.align(17), frame.ip
            //     fmt.align(25), Instruction.pretty_print(frame.code[frame.ip..])
            //     fmt.align(45), frame.dump_stack()
            // ])
        }

        frame.ip += 1

        // decode and execute
        match Instruction(opcode) {
            .Noop -> {}
            
            .Pop -> {
                pop(int)
            }

            .Const -> {
                val := arg(int)
                push(val)
            }
            .AddressOf -> {
                index := arg(int)
                address := cast(int) ^frame.stack[frame.bp + index]
                push(address)
            }
            .Load -> {
                address := pop(^int)
                push(<<address)
            }
            .Store -> {
                address := pop(^int)
                value   := pop(int)
                <<address = value
            }
            .LoadLocal -> {
                index := arg(int)
                push(frame.stack[frame.bp + index])
            }
            .StoreLocal -> {
                index := arg(int)
                frame.stack[frame.bp + index] = pop(int)
            }

            .Add -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(f32, -1, peek(f32, -1) + peek(f32, 0))
                    .F64 -> set(f64, -1, peek(f64, -1) + peek(f64, 0))
                    .I8  -> set(i8,  -1, peek(i8,  -1) + peek(i8,  0))
                    .I16 -> set(i16, -1, peek(i16, -1) + peek(i16, 0))
                    .I32 -> set(i32, -1, peek(i32, -1) + peek(i32, 0))
                    .I64 -> set(i64, -1, peek(i64, -1) + peek(i64, 0))
                    .U8  -> set(u8,  -1, peek(u8,  -1) + peek(u8,  0))
                    .U16 -> set(u16, -1, peek(u16, -1) + peek(u16, 0))
                    .U32 -> set(u32, -1, peek(u32, -1) + peek(u32, 0))
                    .U64 -> set(u64, -1, peek(u64, -1) + peek(u64, 0))
                }
                frame.sp -= 1
            }
            .Sub -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(f32, -1, peek(f32, -1) - peek(f32, 0))
                    .F64 -> set(f64, -1, peek(f64, -1) - peek(f64, 0))
                    .I8  -> set(i8,  -1, peek(i8,  -1) - peek(i8,  0))
                    .I16 -> set(i16, -1, peek(i16, -1) - peek(i16, 0))
                    .I32 -> set(i32, -1, peek(i32, -1) - peek(i32, 0))
                    .I64 -> set(i64, -1, peek(i64, -1) - peek(i64, 0))
                    .U8  -> set(u8,  -1, peek(u8,  -1) - peek(u8,  0))
                    .U16 -> set(u16, -1, peek(u16, -1) - peek(u16, 0))
                    .U32 -> set(u32, -1, peek(u32, -1) - peek(u32, 0))
                    .U64 -> set(u64, -1, peek(u64, -1) - peek(u64, 0))
                }
                frame.sp -= 1
            }
            .Mul -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(f32, -1, peek(f32, -1) * peek(f32, 0))
                    .F64 -> set(f64, -1, peek(f64, -1) * peek(f64, 0))
                    .I8  -> set(i8,  -1, peek(i8,  -1) * peek(i8,  0))
                    .I16 -> set(i16, -1, peek(i16, -1) * peek(i16, 0))
                    .I32 -> set(i32, -1, peek(i32, -1) * peek(i32, 0))
                    .I64 -> set(i64, -1, peek(i64, -1) * peek(i64, 0))
                    .U8  -> set(u8,  -1, peek(u8,  -1) * peek(u8,  0))
                    .U16 -> set(u16, -1, peek(u16, -1) * peek(u16, 0))
                    .U32 -> set(u32, -1, peek(u32, -1) * peek(u32, 0))
                    .U64 -> set(u64, -1, peek(u64, -1) * peek(u64, 0))
                }
                frame.sp -= 1
            }
            .Div -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(f32, -1, peek(f32, -1) / peek(f32, 0))
                    .F64 -> set(f64, -1, peek(f64, -1) / peek(f64, 0))
                    .I8  -> set(i8,  -1, peek(i8,  -1) / peek(i8,  0))
                    .I16 -> set(i16, -1, peek(i16, -1) / peek(i16, 0))
                    .I32 -> set(i32, -1, peek(i32, -1) / peek(i32, 0))
                    .I64 -> set(i64, -1, peek(i64, -1) / peek(i64, 0))
                    .U8  -> set(u8,  -1, peek(u8,  -1) / peek(u8,  0))
                    .U16 -> set(u16, -1, peek(u16, -1) / peek(u16, 0))
                    .U32 -> set(u32, -1, peek(u32, -1) / peek(u32, 0))
                    .U64 -> set(u64, -1, peek(u64, -1) / peek(u64, 0))
                }
                frame.sp -= 1
            }
            .Mod -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(f32, -1, peek(f32, -1) % peek(f32, 0))
                    .F64 -> set(f64, -1, peek(f64, -1) % peek(f64, 0))
                    .I8  -> set(i8,  -1, peek(i8,  -1) % peek(i8,  0))
                    .I16 -> set(i16, -1, peek(i16, -1) % peek(i16, 0))
                    .I32 -> set(i32, -1, peek(i32, -1) % peek(i32, 0))
                    .I64 -> set(i64, -1, peek(i64, -1) % peek(i64, 0))
                    .U8  -> set(u8,  -1, peek(u8,  -1) % peek(u8,  0))
                    .U16 -> set(u16, -1, peek(u16, -1) % peek(u16, 0))
                    .U32 -> set(u32, -1, peek(u32, -1) % peek(u32, 0))
                    .U64 -> set(u64, -1, peek(u64, -1) % peek(u64, 0))
                }
                frame.sp -= 1
            }

            .Not -> {
                v := pop(bool)
                push(!v)
            }
            .Neg -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(f32, 0, -peek(f32, 0))
                    .F64 -> set(f64, 0, -peek(f64, 0))
                    .I8  -> set(i8,  0, -peek(i8,  0))
                    .I16 -> set(i16, 0, -peek(i16, 0))
                    .I32 -> set(i32, 0, -peek(i32, 0))
                    .I64 -> set(i64, 0, -peek(i64, 0))
                    .U8  -> set(u8,  0, -peek(u8,  0))
                    .U16 -> set(u16, 0, -peek(u16, 0))
                    .U32 -> set(u32, 0, -peek(u32, 0))
                    .U64 -> set(u64, 0, -peek(u64, 0))
                }
            }

            .Ieq -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(bool, -1, peek(f32, -1) == peek(f32, 0))
                    .F64 -> set(bool, -1, peek(f64, -1) == peek(f64, 0))
                    .I8  -> set(bool, -1, peek(i8,  -1) == peek(i8,  0))
                    .I16 -> set(bool, -1, peek(i16, -1) == peek(i16, 0))
                    .I32 -> set(bool, -1, peek(i32, -1) == peek(i32, 0))
                    .I64 -> set(bool, -1, peek(i64, -1) == peek(i64, 0))
                    .U8  -> set(bool, -1, peek(u8,  -1) == peek(u8,  0))
                    .U16 -> set(bool, -1, peek(u16, -1) == peek(u16, 0))
                    .U32 -> set(bool, -1, peek(u32, -1) == peek(u32, 0))
                    .U64 -> set(bool, -1, peek(u64, -1) == peek(u64, 0))
                }
                frame.sp -= 1
            }
            .Ine -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(bool, -1, peek(f32, -1) != peek(f32, 0))
                    .F64 -> set(bool, -1, peek(f64, -1) != peek(f64, 0))
                    .I8  -> set(bool, -1, peek(i8,  -1) != peek(i8,  0))
                    .I16 -> set(bool, -1, peek(i16, -1) != peek(i16, 0))
                    .I32 -> set(bool, -1, peek(i32, -1) != peek(i32, 0))
                    .I64 -> set(bool, -1, peek(i64, -1) != peek(i64, 0))
                    .U8  -> set(bool, -1, peek(u8,  -1) != peek(u8,  0))
                    .U16 -> set(bool, -1, peek(u16, -1) != peek(u16, 0))
                    .U32 -> set(bool, -1, peek(u32, -1) != peek(u32, 0))
                    .U64 -> set(bool, -1, peek(u64, -1) != peek(u64, 0))
                }
                frame.sp -= 1
            }
            .Ilt -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(bool, -1, peek(f32, -1) < peek(f32, 0))
                    .F64 -> set(bool, -1, peek(f64, -1) < peek(f64, 0))
                    .I8  -> set(bool, -1, peek(i8,  -1) < peek(i8,  0))
                    .I16 -> set(bool, -1, peek(i16, -1) < peek(i16, 0))
                    .I32 -> set(bool, -1, peek(i32, -1) < peek(i32, 0))
                    .I64 -> set(bool, -1, peek(i64, -1) < peek(i64, 0))
                    .U8  -> set(bool, -1, peek(u8,  -1) < peek(u8,  0))
                    .U16 -> set(bool, -1, peek(u16, -1) < peek(u16, 0))
                    .U32 -> set(bool, -1, peek(u32, -1) < peek(u32, 0))
                    .U64 -> set(bool, -1, peek(u64, -1) < peek(u64, 0))
                }
                frame.sp -= 1
            }
            .Ile -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(bool, -1, peek(f32, -1) <= peek(f32, 0))
                    .F64 -> set(bool, -1, peek(f64, -1) <= peek(f64, 0))
                    .I8  -> set(bool, -1, peek(i8,  -1) <= peek(i8,  0))
                    .I16 -> set(bool, -1, peek(i16, -1) <= peek(i16, 0))
                    .I32 -> set(bool, -1, peek(i32, -1) <= peek(i32, 0))
                    .I64 -> set(bool, -1, peek(i64, -1) <= peek(i64, 0))
                    .U8  -> set(bool, -1, peek(u8,  -1) <= peek(u8,  0))
                    .U16 -> set(bool, -1, peek(u16, -1) <= peek(u16, 0))
                    .U32 -> set(bool, -1, peek(u32, -1) <= peek(u32, 0))
                    .U64 -> set(bool, -1, peek(u64, -1) <= peek(u64, 0))
                }
                frame.sp -= 1
            }
            .Igt -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(bool, -1, peek(f32, -1) > peek(f32, 0))
                    .F64 -> set(bool, -1, peek(f64, -1) > peek(f64, 0))
                    .I8  -> set(bool, -1, peek(i8,  -1) > peek(i8,  0))
                    .I16 -> set(bool, -1, peek(i16, -1) > peek(i16, 0))
                    .I32 -> set(bool, -1, peek(i32, -1) > peek(i32, 0))
                    .I64 -> set(bool, -1, peek(i64, -1) > peek(i64, 0))
                    .U8  -> set(bool, -1, peek(u8,  -1) > peek(u8,  0))
                    .U16 -> set(bool, -1, peek(u16, -1) > peek(u16, 0))
                    .U32 -> set(bool, -1, peek(u32, -1) > peek(u32, 0))
                    .U64 -> set(bool, -1, peek(u64, -1) > peek(u64, 0))
                }
                frame.sp -= 1
            }
            .Ige -> {
                typ := arg(InstNumTyp)
                match typ {
                    .F32 -> set(bool, -1, peek(f32, -1) >= peek(f32, 0))
                    .F64 -> set(bool, -1, peek(f64, -1) >= peek(f64, 0))
                    .I8  -> set(bool, -1, peek(i8,  -1) >= peek(i8,  0))
                    .I16 -> set(bool, -1, peek(i16, -1) >= peek(i16, 0))
                    .I32 -> set(bool, -1, peek(i32, -1) >= peek(i32, 0))
                    .I64 -> set(bool, -1, peek(i64, -1) >= peek(i64, 0))
                    .U8  -> set(bool, -1, peek(u8,  -1) >= peek(u8,  0))
                    .U16 -> set(bool, -1, peek(u16, -1) >= peek(u16, 0))
                    .U32 -> set(bool, -1, peek(u32, -1) >= peek(u32, 0))
                    .U64 -> set(bool, -1, peek(u64, -1) >= peek(u64, 0))
                }
                frame.sp -= 1
            }

            .And -> {
                b, a := pop(bool), pop(bool)
                push(a and b)
            }
            .Or -> {
                b, a := pop(bool), pop(bool)
                push(a or b)
            }

            .Branch -> {
                frame.ip = arg(int)
            }
            .BranchIf -> {
                address := arg(int)
                if pop(int) != 0 {
                    frame.ip = address
                }
            }

            .NativeCall -> {
                func := FatFunctions.from_pointers(NativeFunction, arg(^void), arg(^void))
                arity := arg(int)

                push_native_frame(arity)
                
                if const DEBUG_INTERPRETER {{
                    frame := top_frame()
                    g_logger.log("{}{}{}{}{}{}{}{}", [
                        fmt.align(0), fp
                        fmt.align(5), frame.sp
                        fmt.align(10), "EnterNativeFunction"
                        fmt.align(35), frame.dump_stack()
                    ], "stdout")
                }}
                func(^self, arity)
                if const DEBUG_INTERPRETER {{
                    frame := top_frame()
                    g_logger.log("{}{}{}{}{}{}{}{}", [
                        fmt.align(0), fp
                        fmt.align(5), frame.sp
                        fmt.align(10), "ExitNativeFunction"
                        fmt.align(35), frame.dump_stack()
                    ], "stdout")
                }}
                pop_frame()
            }
            .CompCall -> {
                func := arg(^AstFunction)
                _ := arg(int)
                _ := arg(int)
                wait_until(func.bytecode != null, {
                    error_handler.report_error("Failed to execute bytecode")
                    error_handler.report_error("Failed to execute bytecode: {}{}{}{}{}{}{}{}{}{}{}{}", [
                        fmt.align(30), frame.func.debug_info.get_line_for_ip(frame.ip_prev)
                        fmt.align(35), fp
                        fmt.align(40), frame.sp
                        fmt.align(46), frame.ip_prev
                        fmt.align(54), Instruction.pretty_print(frame.code[frame.ip_prev..])
                        fmt.align(74), frame.dump_stack()
                    ])
                    return .Done
                })
                push_frame(func.bytecode, func.params.count())
            }

            .Return -> {
                @assert(frame.sp > 0)
                frame.stack[0] = frame.stack[frame.sp - 1]
                pop_frame()
                if fp >= 0 {
                    // increment sp by one for return value
                    // @todo: needs to work with return values bigger than 8 bytes (1 stack slot)
                    // @fixme frames[fp].sp += 1 // fails to compile
                    (^frames[fp]).sp += 1
                } else {
                    return .Done
                }
            }
            .ReturnVoid -> {
                pop_frame()
                if fp < 0 {
                    return .Done
                }
            }

            .Panic -> {
                str := string(arg(^u8), arg(int))
                file := frame.func.debug_info.path.slice()
                line := frame.func.debug_info.get_line_for_ip(frame.ip_prev)
                g_logger.log("[PANIC] {}:{}: {}", [file, line, str], "stderr")
                return .Done
            }

            .Halt -> {
                return .Done
            }

            $unknown -> {
                g_logger.log("{}: Unknown Instruction '{}'", [
                    frame.func.debug_info.get_line_for_ip(frame.ip_prev)
                    Instruction.pretty_print(frame.code[frame.ip_prev..])
                ], "stdout")
            }
        }

        // frame = ^frames[fp]
        // line := frame.func.debug_info.get_line_for_ip(frame.ip)
        // g_logger.log("{}{}{}{}{}{}{}{}{}{}{}", [
        //     line
        //     fmt.align(6), fp
        //     fmt.align(11), frame.sp
        //     fmt.align(17), frame.ip
        //     fmt.align(25), Instruction.pretty_print(frame.code[frame.ip..])
        //     fmt.align(45), frame.dump_stack()
        // ], "stdout")

        return .Continue
    }

    run :: (&Self) {
        @assert(fp >= 0)
        frame := ^frames[fp]
        while frame.ip >= 0 and frame.ip < frame.code.length {
            if step() == .Done {
                return
            }
        }
    }
}

InstNumTyp :: enum {
    F32, F64
    I8, I16, I32, I64
    U8, U16, U32, U64
}

Instruction :: enum #repr("C") #tag_type(u64) {
    Noop

    Pop
    Const
    AddressOf
    Load, Store
    LoadLocal, StoreLocal

    Add, Sub, Mul, Div, Mod
    Ieq, Ine, Ilt, Ile, Igt, Ige
    Neg

    Not
    And, Or

    Branch, BranchIf

    Call, NativeCall, CompCall
    Return, ReturnVoid

    Panic
    Halt
}

impl Instruction {
    pretty_print :: (code: []u64) -> String {
        @assert(code.length > 0)

        arg :: (T: type, index: int) #macro {
            <<cast(^T)^code[index + 1]
        }

        return match Instruction(code[0]) {
            .Noop           -> fmt.format("Noop")

            .Pop            -> fmt.format("Pop")
            .Const          -> fmt.format("Const {}", [arg(int, 0)])
            .AddressOf      -> fmt.format("AddressOf {}", [arg(int, 0)])
            .Load           -> fmt.format("Load")
            .Store          -> fmt.format("Store")
            .LoadLocal      -> fmt.format("LoadLocal {}", [arg(int, 0)])
            .StoreLocal     -> fmt.format("StoreLocal {}", [arg(int, 0)])

            .Add            -> fmt.format("Add {}", [arg(InstNumTyp, 0)])
            .Sub            -> fmt.format("Sub {}", [arg(InstNumTyp, 0)])
            .Mul            -> fmt.format("Mul {}", [arg(InstNumTyp, 0)])
            .Div            -> fmt.format("Div {}", [arg(InstNumTyp, 0)])
            .Mod            -> fmt.format("Mod {}", [arg(InstNumTyp, 0)])

            .Not            -> fmt.format("Not")
            .Neg            -> fmt.format("Neg {}", [arg(InstNumTyp, 0)])

            .Ieq            -> fmt.format("Ieq {}", [arg(InstNumTyp, 0)])
            .Ine            -> fmt.format("Ine {}", [arg(InstNumTyp, 0)])
            .Ilt            -> fmt.format("Ilt {}", [arg(InstNumTyp, 0)])
            .Ile            -> fmt.format("Ile {}", [arg(InstNumTyp, 0)])
            .Igt            -> fmt.format("Igt {}", [arg(InstNumTyp, 0)])
            .Ige            -> fmt.format("Ige {}", [arg(InstNumTyp, 0)])

            .And            -> fmt.format("And")
            .Or             -> fmt.format("Or")
            
            .Branch         -> fmt.format("Branch {}", [arg(int, 0)])
            .BranchIf       -> fmt.format("BranchIf {}", [arg(int, 0)])

            .Call           -> fmt.format("Call {} {}", [string(arg(^u8, 0), arg(int, 1)), arg(int, 2)])
            .NativeCall     -> fmt.format("NativeCall {2}", [arg(^void, 0), arg(^void, 1), arg(int, 2)])
            .CompCall       -> fmt.format("CompCall #{}", [arg(^AstFunction, 0).id])
            .Return         -> fmt.format("Return")
            .ReturnVoid     -> fmt.format("ReturnVoid")

            .Halt           -> fmt.format("Halt")
            .Panic          -> fmt.format("Panic `"{}`"", [string(arg(^u8, 0), arg(int, 1))])
            _               -> fmt.format("Unknown {}", [code[0]])
        }
    }
}