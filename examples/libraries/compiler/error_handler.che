use import std.math
use import std.string
use import std.thread

fmt :: import std.fmt
io  :: import std.io
mem :: import std.mem.allocator

use import lexer

#export_scope

ErrorHandler :: trait {
    text_provider : ^TextProvider
    has_errors :: (&Self) -> bool;
    report_error :: (&Self, message: string, args: []^any = [], prefix: string = "", details: [](fmt: string, args: []^any) = []);
    report_error_at :: (&Self, location: Location, message: string, args: []^any = [], prefix: string = "", details: [](fmt: string, args: []^any) = []);
}

TextProvider :: trait {
    get_text :: (&Self, filename: string) -> string;
}

PrefixErrorHandler :: struct ErrorHandler {
    base    : &ErrorHandler
    prefix  : String
}

impl PrefixErrorHandler {
    new :: (base: &ErrorHandler, prefix: String) -> ^PrefixErrorHandler {
        eh := mem.alloc(PrefixErrorHandler)
        *eh = PrefixErrorHandler(base.text_provider, base, prefix)
        return eh
    }
}

impl ErrorHandler for PrefixErrorHandler {

    has_errors :: (&Self) -> bool {
        return base.has_errors()
    }

    report_error :: (&Self, message: string, args: []^any = [], prefix: string = "", details: [](fmt: string, args: []^any) = []) {
        pre := if prefix != "" {
            fmt.format("{}][{}", [self.prefix, prefix])
        } else {
            self.prefix.clone()
        }
        base.report_error(message, args, pre.slice(), details)
    }

    report_error_at :: (&Self, location: Location, message: string, args: []^any = [], prefix: string = "", details: [](fmt: string, args: []^any) = []) {
        pre := if prefix != "" {
            fmt.format("{}][{}", [self.prefix, prefix])
        } else {
            self.prefix.clone()
        }
        base.report_error_at(location, message, args, pre.slice(), details)
    }
}

ConsoleErrorHandler :: struct ErrorHandler {
    error_count         := 0
    do_print_location   := true
    code_color_error    := io.ConsoleColor.Red
    code_color_normal   := io.ConsoleColor.Green
    mutex               := Mutex.new()
}

impl ConsoleErrorHandler {
    new :: (text_provider: ^TextProvider) -> ConsoleErrorHandler {
        return ConsoleErrorHandler(text_provider)
    }
}

impl ErrorHandler for ConsoleErrorHandler {
    has_errors :: (&Self) -> bool {
        return error_count > 0
    }

    report_error :: (&Self, message: string, args: []^any = [], prefix: string = "", details: [](fmt: string, args: []^any) = []) {
        mutex.lock()
        defer mutex.release()

        error_count += 1

        if prefix != "" {
            io.set_console_color(@bin_or(io.ConsoleColor.White, io.ConsoleColor.Bright), io.ConsoleColor.Black)
            io.format("[{}] ", [prefix])
        }
        io.set_console_color(@bin_or(io.ConsoleColor.Red, io.ConsoleColor.Bright), io.ConsoleColor.Black)
        io.formatln(message, args)
        io.set_console_color(io.ConsoleColor.White, io.ConsoleColor.Black)
    }

    report_error_at :: (&Self, location: Location, message: string, args: []^any = [], prefix: string = "", details: [](fmt: string, args: []^any) = []) {
        mutex.lock()
        defer mutex.release()

        error_count += 1

        io.set_console_color(@bin_or(io.ConsoleColor.White, io.ConsoleColor.Bright), io.ConsoleColor.Black)
        if prefix != "" {
            io.format("[{}] ", [prefix])
        }
        io.format("{}:{}:{}: ", [location.file, location.line, location.column])
        io.set_console_color(@bin_or(io.ConsoleColor.Red, io.ConsoleColor.Bright), io.ConsoleColor.Black)
        io.formatln(message, args)
        io.set_console_color(io.ConsoleColor.White, io.ConsoleColor.Black)

        if do_print_location then print_location(location)

        if details.length > 0 {
            print_details(details)
        }
    }

    print_details :: (&Self, details: [](fmt: string, args: []^any)) {
        io.set_console_color(@bin_or(io.ConsoleColor.Red, io.ConsoleColor.Green, io.ConsoleColor.Bright), io.ConsoleColor.Black)
        for detail in details {
            io.formatln(detail.fmt, detail.args)
        }
        io.set_console_color(io.ConsoleColor.White, io.ConsoleColor.Black)
    }

    print_location :: (&Self, location: Location) {
        text := text_provider.get_text(location.file)

        // returns index of first character in the same line
        find_start_of_line :: (text: string, byte_index: int) -> int {
            bytes := text.bytes
            start := byte_index
            byte_index -= 1
            while byte_index >= 0 and *bytes[byte_index] != u8('`n') {
                start = byte_index
                byte_index -= 1
            }
            return start
        }

        // returns index of \n at the end of the same line
        find_end_of_line :: (text: string, byte_index: int) -> int {
            bytes := text.bytes
            while byte_index < text.bytes.length and *bytes[byte_index] != u8('`n') {
                byte_index += 1
            }
            return byte_index
        }

        // 
        get_line_range :: (text: string, start_line: int, byte_index: int, byte_length: int) -> RangeInclusive[int] {
            bytes    := text.bytes
            end_line := start_line
            end      := byte_index + byte_length

            while byte_index < end, byte_index += 1 {
                if *bytes[byte_index] == u8('`n') {
                    end_line += 1
                }
            }

            return start_line ..= end_line
        }

        print_line_number :: (line: int, max_line_digits: int) {
            line_digits := count_digits(line)
            for 0 .. max_line_digits - line_digits {
                io.print(" ")
            }
            io.print(line)
            io.print("> ")
        }

        // print line containing error
        {
            // byte range of entire error location
            total_range := location.byte_index .. location.byte_index + location.byte_length

            lines := get_line_range(text, location.line, location.byte_index, location.byte_length)
            max_line_digits := count_digits(lines.end)

            index := location.byte_index
            for line in lines {
                // byte range of current line
                line_range  := find_start_of_line(text, index) .. find_end_of_line(text, index)

                // byte range of error in current line
                error_range := index .. min(line_range.end, total_range.end)

                sub1 := text[line_range.start  .. error_range.start]
                sub2 := text[error_range.start .. error_range.end]
                sub3 := text[error_range.end   .. line_range.end]

                sub1_bytes := sub1.bytes
                sub2_bytes := sub2.bytes
                sub3_bytes := sub3.bytes

                print_stuff :: (bytes: []u8) #macro {
                    for b in bytes do io.printf("{:x} ", b)
                }

                print_line_number(line, max_line_digits)
                io.set_console_color(code_color_normal, io.ConsoleColor.Black)
                io.print(sub1)
                io.set_console_color(@bin_or(code_color_error, io.ConsoleColor.Bright), io.ConsoleColor.Black)
                io.print(sub2)
                io.set_console_color(code_color_normal, io.ConsoleColor.Black)
                io.print(sub3)
                io.set_console_color(io.ConsoleColor.White, io.ConsoleColor.Black)
                io.println("")

                index = line_range.end + 1
            }
        }
    }
}