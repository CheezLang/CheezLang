use import std.array
use import std.string
use import std.os.windows_functions
use import std.mem.allocator
use import std.util
io  :: import std.io
fmt :: import std.fmt
C   :: import std.c

console_debug_print :: false
indent_size         :: 2

#export_scope

Clap :: struct {

}

Location :: struct #copy {
    start   : int
    end     : int
}

impl Location {
    contains :: (ref Self, pos: int) -> bool {
        return pos >= start and pos <= end
    }

    before :: (ref Self, pos: int) -> bool {
        return pos >= end
    }
}

ClapToken :: struct {
    loc : Location
    raw : String
}

impl Clap {
    tokenize :: (str: string) -> Array[ClapToken] {
        args := Array[ClapToken].create()

        State :: enum #copy {
            Unquoted, Whitespace, Escaped, Quoted
        }
        use State
        state := Whitespace
        start := 0

        loc := Location(0, 0)
        current := String.empty()

        for c : str {
            match state, c {
                (Escaped, $c) -> {
                    state = Quoted
                    current += c
                }

                (Unquoted, $c) if char.is_whitespace(c) -> {
                    args.add(ClapToken(loc, current.clone()))
                    current.clear()
                    state = Whitespace
                }
                (Unquoted, $c) -> {
                    current += c
                }

                (Whitespace, '"') -> {
                    state = Quoted

                    loc.start = it_index + 1
                    loc.end = loc.start
                }
                (Whitespace, $c) if !char.is_whitespace(c) -> {
                    current += c
                    state = Unquoted

                    loc.start = it_index
                    loc.end = loc.start
                }

                (Quoted, '"') -> {
                    loc.end -= 1
                    args.add(ClapToken(loc, current.clone()))
                    current.clear()
                    state = Whitespace
                }

                (Quoted, '``') -> {
                    state = Escaped
                }

                (Quoted, $c) -> {
                    current += c
                }
            }

            loc.end += 1
        }

        if state == Unquoted {
            args.add(ClapToken(loc, current.clone()))
            current.clear()
        }

        return args
    }
}

parse_arguments_from_string :: ($T: type, str: string) -> &T {
    args := Array[String].create()

    State :: enum #copy {
        Unquoted, Whitespace, Escaped, Quoted
    }
    use State
    state := Whitespace
    start := 0

    current := String.empty()

    for c : str {
        match state, c {
            (Escaped, $c) -> {
                state = Quoted
                current += c
            }

            (Unquoted, $c) if char.is_whitespace(c) -> {
                args.add(current.clone())
                current.clear()
                state = Whitespace
            }
            (Unquoted, $c) -> {
                current += c
            }

            (Whitespace, '"') -> {
                state = Quoted
            }
            (Whitespace, $c) if !char.is_whitespace(c) -> {
                current += c
                state = Unquoted
            }

            (Quoted, '"') -> {
                args.add(current.clone())
                current.clear()
                state = Whitespace
            }

            (Quoted, '``') -> {
                state = Escaped
            }

            (Quoted, $c) -> {
                current += c
            }
        }
    }

    if state == Unquoted {
        // bytes := str.bytes[start .. str.bytes.length]
        // args.add(String.from_string(string(bytes.data, bytes.length)))
        args.add(current.clone())
        current.clear()
    }

    // io.println(fmt.format("===`n{}`n===", [args.slice()]))

    return parse_arguments(T, args.slice())
}

parse_arguments :: ($T: type, args: []String) -> &T {
    result := alloc(T)
    ok := parse_arguments_help(@type_info(T), args, result)

    if !ok {
        Memory.drop(<<result)
        free(result)
        return null
    }
    return result
}

get_cmd_args :: () -> Array[String] {
    cmdLineArgs := GetCommandLineW()
    count := 0i32
    args := CommandLineToArgvW(cmdLineArgs, &count)
    return to_ascii_strings(args, int(count))
}

get_help_message :: (type_info: &TypeInfo) -> String {
    result := String.empty()
    result += "Usage:`n"

    get_help_message_helper(type_info, result, indent_size)

    return result
}

#file_scope

parse_arguments_help :: (type_info: &TypeInfo, args: ref []String, result: &$T) -> bool {
    if const console_debug_print {
        io.formatln("parsing help: {}", [type_info.kind])
        log_args(args)
    }

    require_arg :: (args: []String) -> bool {
        if args.length < 1 {
            io.println("[ERROR] missing required argument for option")
            return false
        }

        return true
    }

    return match type_info.kind {
        // String
        _ if type_info == @type_info(String) -> {
            if !require_arg(args) { return false }
            <<cast(&String) result = args[0].clone()
            args = args[1..]
            true
        }

        // Array[String]
        _ if type_info == @type_info(Array[String]) -> parse_arguments_string_array(args, result)

        // Option[String]
        _ if type_info == @type_info(Option[String]) -> {
            temp := @alloca(String, 1)
            parse_arguments_help(@type_info(String), args, temp.data)
            <<cast(&Option[String]) result = Some(<<temp.data)
            true
        }

        TypeInfoKind.Enum($info)   -> parse_arguments_enum(&info, args, result)
        TypeInfoKind.Struct($info) -> parse_arguments_struct(&info, args, result)
        TypeInfoKind.Int($_)    -> {
            if !require_arg(args) { return false }
            <<cast(&int) result = fmt.parse_int(args[0].slice())
            args = args[1..]
            true
        }

        TypeInfoKind.Bool -> {
            <<cast(&bool) result = true
            true
        }
        _ -> {
            io.printfln("[ERROR] Can't parse into type {}", @typename(T))
            false
        }
    }
}

parse_arguments_string_array :: (args: ref []String, result: &void) -> bool {
    if const console_debug_print {
        io.println("parse_arguments_string_array")
        log_args(args)
    }

    array := cast(&Array[String]) result
    <<array = Array[String].create()

    while args.length > 0 {
        arg := ref args[0]
        @assert(arg.length > 0)

        if arg[0] == '-' {
            return true
        }

        array.add(arg.clone())
        args = args[1..]
    }

    return true
}

parse_arguments_enum :: (type_info: &TypeInfoEnum, args: ref []String, result: &void) -> bool {
    if const console_debug_print {
        io.printfln("parsing into enum {}", type_info.name)
        log_args(args)
    }

    if args.length == 0 {
        io.println("[ERROR] Missing command. Possible commands:")
        for(by_ref=true) type_info.members {
            io.printfln("  {}", get_display_name_for_member(it))
        }
        return false
    }

    command := ref args[0]
    args = args[1..]

    for(by_ref=true) mem : type_info.members {
        mem_name := get_display_name_for_member(mem)
        if streq(command.slice(), mem_name) {
            // first store the tag
            tag_ptr := cast(&int) result
            <<tag_ptr = mem.tag

            if mem.typ != null {
                // parse remaining arguments into associated value
                tag_size := type_info.tag_type.size
                value_ptr := pointer_add(result, tag_size)

                if !parse_arguments_help(mem.typ, args, value_ptr) {
                    return false
                }
            }
            return true
        }
    }

    C.puts("3")
    io.printfln("[ERROR] Unknown command: {}", command.slice())
    C.puts("4")
    return false
}

parse_arguments_struct :: (type_info: &TypeInfoStruct, args: ref []String, result: &void) -> bool {
    if const console_debug_print {
        io.printfln("parsing into struct {}", type_info.name)
        log_args(args)
    }

    // filter members of struct + handle #name and #ignore
    MemberInfo :: (name: string, mem: &TypeInfoStructMember, set: bool)
    members := Array[MemberInfo].create()
    for(by_ref=true) m : type_info.members {
        mem_name := get_display_name_for_member(m)

        if m.has_attribute("ignore") {
            // io.printfln("ignoring '{}'", m.name)
            continue
        }

        members.add((mem_name, &m, false))
    }

    get_member :: (arr: ref @typeof(members), name: string) -> &MemberInfo {
        for(by_ref=true) arr.slice() {
            if streq(it.name, name) {
                return &it
            }
        }
        return null
    }

    // parse arguments
    while args.length > 0 {
        arg := ref args[0]
        @assert(arg.length > 0)

        args = args[1..]

        if arg[0] == '-' {
            name := arg.sliceF(1)
            mem_info := get_member(members, name)

            if mem_info == null {
                io.printfln("[ERROR] unknown argument '{}'", name)
                return false
            }

            if mem_info.set {
                io.printfln("[ERROR] option '{}' was already specified", mem_info.name)
            }

            if !parse_arguments_help(mem_info.mem.typ, args, pointer_add(result, mem_info.mem.offset)) {
                return false
            }

            mem_info.set = true
        } else {
            io.printfln("[CLP] Error: not implemented, arg: {}", arg.slice())
            @assert(false, "Not implemented")
            return false
        }
    }

    // check if all members were set or have a default value
    for(by_ref=true) mem_info : members {
        if mem_info.set then continue

        default_value := mem_info.mem.value
        if @type_info_of_any(default_value) == null {
            io.printfln("[ERROR] please provide a value for option '{}'", mem_info.mem.name)
            return false
        }

        if !assign_value_to_member(mem_info.mem, pointer_add(result, mem_info.mem.offset), default_value) {
            return false
        }
    }

    return true
}

get_help_message_helper :: (type_info: &TypeInfo, result: ref String, indent: int) {
    match type_info.kind {
        $_ if type_info == @type_info(String) -> {
            indent_string(result, indent)
            result += "String"
        }
        $_ if type_info == @type_info(Array[String]) -> {
            result += "Array[String]"
        }

        TypeInfoKind.Struct($ti) -> get_help_message_struct(&ti, result, indent)
        TypeInfoKind.Enum($ti) -> get_help_message_enum(&ti, result, indent)
    }
}

get_help_message_struct :: (type_info: &TypeInfoStruct, result: ref String, indent: int) {
    old_indent := indent
    indent += indent_size

    description_indent := {
        m := 0
        for(by_ref=true) mem : type_info.members {
            if mem.name.bytes.length > m {
                m = mem.name.bytes.length
            }
        }
        next_aligned_num(m + 1 + 8, indent_size)
    }

    // print descriptions
    for(by_ref=true) mem : type_info.members {
        indent_string(result, indent)
        

        mem_name := get_display_name_for_member(mem)
        result.appendf("-{}", mem_name)

        help_text_att := mem.get_attribute("help")
        if help_text_att != null {
            @assert(help_text_att.args.length == 1)
            @assert(@type_info_of_any(help_text_att.args[0]) == @type_info(string))
            text := String.from_string(<<cast(&string) @ptr_of_any(help_text_att.args[0]))

            lines := text.split_at_char('`n', true)

            for line : lines {
                if it_index == 0 {
                    indent_string(result, description_indent - mem_name.bytes.length - 1)
                } else {
                    result += "`n"
                    indent_string(result, description_indent + indent)
                }
                result += line
            }
        }

        result += "`n"
    }

    // print detailed info (args)

    if type_info.members.length > 0 {
        result += "`n"
        for(by_ref=true) mem : type_info.members {
            mem_name := get_display_name_for_member(mem)
            get_help_message_member(result, mem.typ, mem_name, "-{}: ", indent, description_indent - mem_name.bytes.length - 3)
            result += "`n"
        }
    }
}

get_help_message_enum :: (type_info: &TypeInfoEnum, result: ref String, indent: int) {
    old_indent := indent
    indent += indent_size

    description_indent := {
        m := 0
        for(by_ref=true) mem : type_info.members {
            if mem.name.bytes.length > m {
                m = mem.name.bytes.length
            }
        }
        next_aligned_num(m + 8, indent_size)
    }

    // print descriptions
    for(by_ref=true) mem : type_info.members {
        indent_string(result, indent)

        mem_name := get_display_name_for_member(mem)
        result += mem_name

        help_text_att := mem.get_attribute("help")
        if help_text_att != null {
            @assert(help_text_att.args.length == 1)
            @assert(@type_info_of_any(help_text_att.args[0]) == @type_info(string))
            text := String.from_string(<<cast(&string) @ptr_of_any(help_text_att.args[0]))

            lines := text.split_at_char('`n', true)

            for line : lines {
                if it_index == 0 {
                    indent_string(result, description_indent - mem_name.bytes.length)
                } else {
                    result += "`n"
                    indent_string(result, description_indent + indent)
                }
                result += line
            }
        }

        result += "`n"
    }

    // print detailed info (args)
    printed_details_string := false
    for(by_ref=true) mem : type_info.members {
        if mem.typ == null then continue

        if !printed_details_string {
            result += "`n"
            printed_details_string = true
        }

        mem_name := get_display_name_for_member(mem)

        get_help_message_member(result, mem.typ, mem_name, "{}: ", indent, description_indent - mem.name.bytes.length - 2)
        result += "`n"
    }
}

get_help_message_member :: (result: ref String, type_info: &TypeInfo, name: string, format: string, indent: int, indent2: int) {
    match type_info.kind {
        $_ if type_info == @type_info(String) -> {
            indent_string(result, indent)
            result.appendf(format, name)
            indent_string(result, indent2)
            result += "String"
        }
        $_ if type_info == @type_info(Array[String]) -> {
            indent_string(result, indent)
            result.appendf(format, name)
            indent_string(result, indent2)
            result += "Array[String]"
        }
        TypeInfoKind.Struct($ti) -> {
            if ti.members.length > 0 {
                indent_string(result, indent)
                result.appendf(format, name)
                result += "`n"
                get_help_message_helper(type_info, result, indent + indent_size)
            }
        }
        TypeInfoKind.Enum($ti) -> {
            if ti.members.length > 0 {
                indent_string(result, indent)
                result.appendf(format, name)
                result += "`n"
                get_help_message_helper(type_info, result, indent + indent_size)
            }
        }
        $_ -> {
            indent_string(result, indent)
            result.appendf(format, name)
            indent_string(result, indent2)
            type_info.print(result, "")
        }
    }
}

assign_value_to_member :: (member: &TypeInfoStructMember, result: &void, value: &any) -> bool {
    if member.typ == @type_info_of_any(value) {
        C.memcpy(result, @ptr_of_any(value), cast member.typ.size)
        return true
    }

    io.printfln("[ERROR] can't assign a value of type ... to ...", ())
    return false
}

// helper functions

to_ascii_strings :: (argv: &&u16, argc: int) -> Array[String] {
    array := Array[String].create(argc)
    for i : 0..argc {
        str := String.empty()
        while k := 0, argv[i][k] != 0, k += 1 {
            str += char(argv[i][k])
        }

        array.add(str)
    }
    return array
}

indent_string :: (str: ref String, amount: int) {
    str.reserve_more(amount)
    for 0..amount {
        str += " "
    }
}

get_display_name_for_member :: (mem: ref $T) -> string {
    name_att := mem.get_attribute("name")
    if name_att == null {
        return mem.name
    }

    @assert(name_att.args.length == 1)
    @assert(@type_info_of_any(name_att.args[0]) == @type_info(string))
    name := <<cast(&string) @ptr_of_any(name_att.args[0])
    return name
}

log_args :: (args: []String) {
    // io.println("=== args ===")
    // io.formatln("len: {}", [args.length])

    // ptr := args.data[0].clone()
    // io.formatln("len: {}", [ptr.length])

    // io.println("1")
    // for(by_ref=false) arg : args {
    //     io.println("2")
    //     slc := arg.slice()
    //     io.println("3")

    //     io.formatln("len: {}", [args[it_index].length])
    //     io.formatln("len: {}", [arg.length])
    //     io.formatln("[{}] '{}'", [it_index, slc])
    //     io.println("4")
    // }
}