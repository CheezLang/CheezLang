use import pixel_game_engine
use import pixel
use import std.math

#export_scope

impl PixelGameEngine {
    clear :: (&Self, p: Pixel = Pixel()) {
        count := draw_target.width * draw_target.height
        pixels := draw_target.get_data()
        for i in 0..count {
            pixels[i] = p
        }
    }

    get_pixel :: (&Self, x: int, y: int) -> Pixel {
        return draw_target.get_pixel(x, y)
    }

    draw :: (&Self, x: int, y: int, p: Pixel) -> bool {
        if draw_target == null {
            return false
        }

        return match nPixelMode {
            PixelMode.Normal -> draw_target.set_pixel(x, y, p)

            PixelMode.Mask if p.a == 255 -> draw_target.set_pixel(x, y, p)

            PixelMode.Alpha -> {
                d := draw_target.get_pixel(x, y)
                a := (cast(float)p.a) / 255.0f * fBlendFactor
                c := 1.0f - a
                r := a * (cast(float)p.r) + c * (cast(float)d.r)
                g := a * (cast(float)p.g) + c * (cast(float)d.g)
                b := a * (cast(float)p.b) + c * (cast(float)d.b)
                draw_target.set_pixel(x, y, Pixel.rgba(cast r, cast g, cast b))
            }
        }
    }

    draw_circle :: (&Self, x: int, y: int, r: int, p: Pixel, mask: u32 = 0xFFFFFFFF) {
        if r == 0 {
            return
        }

        mut x0 := 0
        mut y0 := r
        mut d := 3 - 2 * r
        while y0 >= x0 {
            if @bin_and(mask, 0x01u32) != 0 { draw(x + x0, y - y0, p) }
            if @bin_and(mask, 0x02u32) != 0 { draw(x + y0, y - x0, p) }
            if @bin_and(mask, 0x04u32) != 0 { draw(x + y0, y + x0, p) }
            if @bin_and(mask, 0x08u32) != 0 { draw(x + x0, y + y0, p) }
            if @bin_and(mask, 0x10u32) != 0 { draw(x - x0, y + y0, p) }
            if @bin_and(mask, 0x20u32) != 0 { draw(x - y0, y + x0, p) }
            if @bin_and(mask, 0x40u32) != 0 { draw(x - y0, y - x0, p) }
            if @bin_and(mask, 0x80u32) != 0 { draw(x - x0, y - y0, p) }

            if d < 0 {
                d += 4 * x0 + 6
                x0 += 1
            } else {
                d += 4 * (x0 - y0) + 10
                x0 += 1
                y0 -= 1
            }
        }
    }

    draw_rect :: (&Self, x: int, y: int, w: int, h: int, p: Pixel) {
        draw_line(x, y, x+w, y, p)
        draw_line(x+w, y, x+w, y+h, p)
        draw_line(x+w, y+h, x, y+h, p)
        draw_line(x, y+h, x, y, p)
    }

    fill_rect :: (&Self, mut x: int, mut y: int, mut w: int, mut h: int, p: Pixel) {
        if w < 0 {
            x += w
            w *= -1
        }
        if h < 0 {
            y += h
            h *= -1
        }

        mut x2 := x + w
        mut y2 := y + h

        if x < 0 { x = 0 }
        if x >= screen_width { x = screen_width }
        if y < 0 { y = 0 }
        if y >= screen_height { y = screen_height }

        
        if x2 < 0 { x2 = 0 }
        if x2 >= screen_width { x2 = screen_width }
        if y2 < 0 { y2 = 0 }
        if y2 >= screen_height { y2 = screen_height }

        for i in x..x2 {
            for k in y..y2 {
                draw(i, k, p)
            }
        }
    }

    rol :: (pattern: &mut u32) -> bool {
        mut p := *pattern
        *pattern = @bin_or(@bin_lsl(p, 1u32), @bin_lsr(p, 31u32))
        p = *pattern
        return @bin_and(p, 1u32) != 0
    }

    draw_line :: (&Self, mut x1: int, mut y1: int, mut x2: int, mut y2: int, p: Pixel, pattern: u32 = 0xFFFFFFFF) {
        mut dx := x2 - x1
        mut dy := y2 - y1

        // vertical line
        if dx == 0 {
            if x1 < 0 or x1 >= screen_width {
                return
            }
            if y2 < y1 {
                (y1, y2) = (y2, y1)
            }

            y1 = max(y1, 0)
            y2 = min(y2, screen_height - 1)

            for y in y1 ..= y2 {
                if rol(&pattern) {
                    self.draw(x1, y, p)
                }
            }
            return
        }

        // horizontal line
        if dy == 0 {
            if y1 < 0 or y1 >= screen_height {
                return
            }
            if x2 < x1 {
                (x1, x2) = (x2, x1)
            }

            x1 = max(x1, 0)
            x2 = min(x2, screen_width - 1)

            for x in x1 ..= x2 {
                if rol(&pattern) {
                    draw(x, y1, p)
                }
            }
            return
        }

        // clamp horizontally
        if x1 > x2 {
            (x1, x2) = (x2, x1)
            (y1, y2) = (y2, y1)
        }
        if x2 < 0 or x1 >= screen_width {
            return
        }
        if x1 < 0 {
            slope := double(dy) / double(dx)
            y1 += int(double(-x1) * slope)
            x1 = 0
        }
        if x2 > screen_width - 1 {
            slope := double(dy) / double(dx)
            y2 += int(double(screen_width - 1 - x2) * slope)
            x2 = screen_width - 1
        }

        // clamp vertically
        if y1 > y2 {
            (x1, x2) = (x2, x1)
            (y1, y2) = (y2, y1)
        }
        if y2 < 0 or y1 >= screen_height {
            return
        }
        if y1 < 0 {
            slope := double(dx) / double(dy)
            x1 += int(double(-y1) * slope)
            y1 = 0
        }
        if y2 > screen_height - 1 {
            slope := double(dx) / double(dy)
            x2 += int(double(screen_height - 1 - y2) * slope)
            y2 = screen_height - 1
        }

        // diagonal lines
        //
        dx = x2 - x1
        dy = y2 - y1

        dx1 := abs(dx)
        dy1 := abs(dy)
        mut px := 2 * dy1 - dx1
        mut py := 2 * dx1 - dy1
        if dy1 <= dx1 {
            mut (x, y, xe) := if dx >= 0 {
                (x1, y1, x2)
            } else {
                (x2, y2, x1)
            }

            if rol(&pattern) {
                draw(x, y, p)
            }

            while x < xe {
                x += 1
                if px < 0 {
                    px += 2 * dy1
                } else {
                    if (dx < 0 and dy < 0) or (dx > 0 and dy > 0) {
                        y += 1
                    } else {
                        y -= 1
                    }
                    px += 2 * (dy1 - dx1)
                }
                if rol(&pattern) {
                    draw(x, y, p)
                }
            }
        } else {
            mut (x, y, ye) := if dy >= 0 {
                (x1, y1, y2)
            } else {
                (x2, y2, y1)
            }

            if rol(&pattern) {
                draw(x, y, p)
            }

            while y < ye {
                y += 1
                if py < 0 {
                    py += 2 * dx1
                } else {
                    if (dx < 0 and dy < 0) or (dx > 0 and dy > 0) {
                        x += 1
                    } else {
                        x -= 1
                    }
                    py += 2 * (dx1 - dy1)
                }
                if rol(&pattern) {
                    draw(x, y, p)
                }
            }
        }
    }
}
