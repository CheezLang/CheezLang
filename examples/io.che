#load("string.che")
#load("memory.che")
#load("std/windows.che")

fn PrintString(s: string) {
    let len = strlen(s)
    let bytesWritten: int
    WriteFile(__std_out_handle, s, len, &bytesWritten, 0)
}

fn Println(s: string) {
    let len = strlen(s)
    let bytesWritten: int
    WriteFile(__std_out_handle, s, len, &bytesWritten, 0)
    WriteFile(__std_out_handle, "`n", 1, &bytesWritten, 0)
}

fn PrintChar(c: byte) { 
    let bytesWritten: int
    WriteFile(__std_out_handle, (string)&c, 1, &bytesWritten, 0)
}

fn clearmem(mem: byte^, len: int) {
    let i: int = 0
    while (i < len) {
        mem[i] = 0
        i = i + 1
    }
}

fn PrintDigit(d: int) {
    PrintChar((byte)d + 48)
}

fn PrintInt(num: int) {
    if (num == 0)
    {
        PrintDigit(0)
        return
    }
    let bufferSize: int = 30
    let buff = (byte^)malloc(bufferSize)
    clearmem(buff, bufferSize)

    let neg = false
    if num < 0 {
        neg = true
        num = -num
    }

    let i: int = bufferSize - 2
    while i > 0 and num != 0 {
        let lastDigit = num % 10
        num = num / 10
        buff[i] = (byte)lastDigit + 48
        i = i - 1
    }

    if (neg) {
        buff[i] = 45
        i = i - 1
    }

    let str = (string)((int)buff + i + 1)
    PrintString(str)

    free((void^)buff)
}

fn PrintfData(formatSpecifier: string, data: any) {
    let len = strlen(formatSpecifier)
    if streq(formatSpecifier, "i8") {
        PrintInt((i32)(i8)data)
    }
    else if streq(formatSpecifier, "i16") {
        PrintInt((i32)(i16)data)
    }
    else if streq(formatSpecifier, "i32") {
        PrintInt((i32)data)
    }
    else if streq(formatSpecifier, "i64") {
        PrintInt((i32)(i64)data)
    }
    else if streq(formatSpecifier, "b") {
        let b = (bool)data
        if (b) {
            PrintString("true")
        } else {
            PrintString("false")
        }
    }
    else if streq(formatSpecifier, "s") {
        PrintString((string)data)
    }
    else {
        PrintString("<Unknown format specifier: ")
        PrintString(formatSpecifier)
        PrintString(">")
    }
}

fn PrintlnInt(i: int) {
    PrintInt(i)
    Println("")
}

fn Printf(format: string, data: any) {
    let open = *"{"
    let close = *"}"

    let formatSpecifier = (string)malloc(5)
    let fi: ulong = 0

    let len = (ulong)strlen(format)
    let i: ulong = 0
    let state = 0

    while (i < len) {
        let c = format[i]

        if state == 0 {
            if (c == open) {
                state = 1
            }
            else {
                PrintChar(c)
            }
        }
        else if state == 1 {
            if (c == close) {
                formatSpecifier[fi] = 0
                state = 0
                fi = 0

                PrintfData(formatSpecifier, data)

            } else {
                formatSpecifier[fi] = c
                fi = fi + 1
            }
        }


        i = i + 1
    }

    free((void^)formatSpecifier)
}

fn Printfn(format: string, args: any[]) {
    let open = *"{"
    let close = *"}"

    let argIndex = 0

    let formatSpecifier = (string)malloc(5)
    let fi: ulong = 0

    let len = (ulong)strlen(format)
    let i: ulong = 0
    let state = 0

    while (i < len) {
        let c = format[i]

        if state == 0 {
            if (c == open) {
                state = 1
            }
            else {
                PrintChar(c)
            }
        }
        else if state == 1 {
            if (c == close) {
                formatSpecifier[fi] = 0
                state = 0
                fi = 0

                PrintfData(formatSpecifier, args[argIndex])
                argIndex = argIndex + 1
            } else {
                formatSpecifier[fi] = c
                fi = fi + 1
            }
        }


        i = i + 1
    }

    free((void^)formatSpecifier)
}

fn Printlnf(format: string, data: any) {
    Printf(format, data)
    Println("")
}

fn Printlnfn(format: string, args: any[]) {
    Printfn(format, args)
    Println("")
}
