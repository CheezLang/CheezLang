use import std.array

#export_scope

Iterator :: trait(T: type) {
    next :: (&Self) -> Option[T];
}

// impl(It: type, T: type) It if It : Iterator(T) {
//     max :: (&Self) -> T {
//         max: T

//         while true {
//             match self.next() {
//                 Some($v) if v > max -> {
//                     max = v
//                 }

//                 None -> { break }
//             }
//         }

//         return max
//     }

//     sum :: (&Self) -> T {
//         sum: T

//         while true {
//             match self.next() {
//                 Some($v) -> {
//                     sum += v
//                 }

//                 None -> {
//                     break
//                 }
//             }
//         }

//         return sum
//     }
// }

impl(It: type, T: type) It if It : Iterator[T] {
    skip :: (&Self, count: int) -> Self {
        for 0..count {
            self.next()
        }

        return *self
    }

    take :: (&Self, count: int) -> TakeIterator[T] {
        return TakeIterator[T](^*self, count)
    }

    map :: (&Self, mapFunc: fn (T) -> $X) -> MapIterator[T, X] {
        return MapIterator[T, X](^*self, mapFunc)
    }

    foreach :: (&Self, func: fn (T)) {
        while true {
            match self.next() {
                Some($v) -> func(v)
                None -> {break}
            }
        }
    }

    to_array :: (&Self) -> Array[T] {
        a := Array[T].create()
        while match self.next() {
            Some($v) -> {
                a.add(v)
                true
            }
            None -> false
        }{}
        return a
    }
}

// take
TakeIterator :: struct(T: type) #copy {
    it      : ^Iterator[T]
    count   : int
}

impl(T: type) Iterator[T] for TakeIterator[T] {
    next :: (&Self) -> Option[T] {
        if count > 0 {
            count -= 1
            return it.next()
        } else {
            return None
        }
    }
}

// map
MapIterator :: struct(T: type, X: type) #copy {
    it      : ^Iterator[T]
    mapFunc : fn(T) -> X
}

impl(T: type, X: type) Iterator[X] for MapIterator[T, X] {
    next :: (&Self) -> Option[X] {
        return match it.next() {
            Some($v) -> Some(mapFunc(v))
            None -> None
        }
    }
}

// range
RangeIterator :: struct #copy {
    from    : int
    to      : int
}

impl Iterator[int] for RangeIterator {
    next :: (&Self) -> Option[int] {
        if from > to {
            return None
        }

        n := from
        from += 1
        return Some(n)
    }
}

range :: (start: int, end: int) -> RangeIterator {
    return RangeIterator(start, end)
}
