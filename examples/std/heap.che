
use import std.mem.allocator

Heap :: struct(T: type) {
    allocator: Allocator
    data    : ^T
    capacity: int
    length  : int
}

impl(T: type) Drop for Heap[T] {
    drop :: (&Self) {
        for 0..length {
            Memory.drop(data[it])
        }
        if allocator != null {
            free(data, allocator)
        }
    }
}

impl(T: type) Heap[T] {
    create :: (allocator: Allocator = DEFAULT_STD_HEAP_ALLOCATOR) -> Heap[T] {
        return Heap[T](
            allocator   = allocator
            data        = alloc_raw(T, 10, allocator)
            capacity    = 10
            length      = 0
        )
    }

    get_length :: (&Self) -> int {
        return length
    }

    reserve :: (&Self, size: int) {
        if capacity > size {
            return
        }

        capacity = size
        data = realloc_raw(data, cast size, allocator)

        if size > length {
            c_memset(cast ^data[length], 0, cast(uint) ((size - length) * @sizeof(T)))
        }

        if length > capacity {
            length = capacity
        }
    }

    get_child_index :: (i: int) -> (left: int, right: int) {
        left = 2 * i + 1
        right = 2 * i + 2
    }

    get_parent_index :: (i: int) -> int {
        return (i - 1) / 2
    }

    insert :: (&Self, val: T) {
        if capacity <= length {
            reserve(length * 2)
        }

        data[length] = val
        length += 1
        
        sift_up(length - 1)
    }

    peek :: (&Self) -> &T {
        @assert(length > 0)
        return data[0]
    }

    delete_first :: (&Self) -> T {
        @assert(length > 0)
        val := data[0]
        length -= 1

        if length > 0 {
            data[0] = data[length]
            sift_down(0)
        }

        return val
    }

    sift_up :: (&Self, i: int) {
        if i == 0 {
            return
        }


        pi := get_parent_index(i)
        if data[i] < data[pi] {
            swap(i, pi)
            sift_up(pi)
        }
    }

    sift_down :: (&Self, i: int) {
        ci := get_child_index(i)

        if ci.left >= length {
            return
        }

        if ci.right >= length {
            if !(data[i] < data[ci.left]) {
                swap(i, ci.left)
                return
            }
        }

        if !(data[i] < data[ci.left]) and data[ci.left] <= data[ci.right] {
            swap(i, ci.left)
            sift_down(ci.left)
        } else if !(data[i] < data[ci.right]) and data[ci.right] <= data[ci.left] {
            swap(i, ci.right)
            sift_down(ci.right)
        }
    }

    swap :: (&Self, a: int, b: int) {
        (data[a], data[b]) = (data[b], data[a])
    }

    slice :: (&Self) -> []T {
        slc : []T = data
        slc.length = length
        return slc
    }

    validate :: (&Self) -> bool {
        for 0..length {
            current := data[it]
            ci := get_child_index(it)

            if ci.left < length {
                if data[ci.left] < data[it] {
                    return false
                }
            }
            if ci.right < length {
                if data[ci.right] < data[it] {
                    return false
                }
            }
        }

        return true
    }
}