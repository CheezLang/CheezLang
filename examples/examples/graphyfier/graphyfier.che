//*

io :: import std.io
C :: import std.c
use import std.io.file
use import std.math
use import std.thread
use import std.profiling
use import std.array
use import std.string
use import std.mem.allocator
use import std.mem.arena_allocator
use import std.os.windows_functions
use import std.os.windows_types
use import std.hash_table

use import evaluator
use import compiler

use import olc_pge.pixel_game_engine
use import olc_pge.pge_helper_functions
use import olc_pge.pge_render_functions
use import olc_pge.pixel
use import olc_pge.key

#export_scope

enable_profiler :: true

vec2 :: Vector2[double]
vec3 :: Vector3[double]

width  : int = 1600
height : int = 960

MyGame :: struct {
    pge                 : &PixelGameEngine = null

    xc                  : double = 0.0
    yc                  : double = 3.0
    xr                  : double = 10.0
    yr                  : double = 10.0

    func_index          : int = 0

    last_mouse_pos      : (x: double, y: double) = (0.0, 0.0)

    function            : Option[Array[OpCode]] = Option[Array[OpCode]].None

    var_name_allocator  : ArenaAllocator

    variables           : Variables

    // auto reload stuff
    dir_change_thread   : &Thread = null
    wait_handle         : HANDLE = null
    last_file_change    : double = -1
}

dir_change_thread_func :: (game: &MyGame) {
    while true {
        res := WaitForSingleObject(game.wait_handle, 1000)
        if res == 0 {
            game.file_changed()
            FindNextChangeNotification(game.wait_handle)
        }
    }
}

map :: (x: double, a: double, b: double, c: double, d: double) -> double {
    return (x - a) / (b - a) * (d - c) + c
}

impl Game for MyGame {
    on_create :: (ref Self, pge: &PixelGameEngine) -> bool {
        profile_function()
        self.pge = pge

        load_function_from_file()

        variables["pi"] = Complex(3.14159265359)
        variables["e"]  = Complex(2.71828182845)
        variables["t"]  = Complex(0.0)

        // start thread for file changes
        fullpath := C._fullpath(null, "."c, 512)
        defer free(fullpath)
        wait_handle = FindFirstChangeNotificationA(fullpath, 0, 0x00000008)
        if wait_handle == null {
            io.printfln("Failed to create file change watcher for file '{}'", string(cast fullpath, cast C.strlen(fullpath)))
        } else {
            dir_change_thread = alloc(Thread)
            <<dir_change_thread = Thread.create(dir_change_thread_func, &self)
            dir_change_thread.start()
        }
        return true
    }

    on_destroy :: (ref Self) -> bool {
        profile_function()
        if dir_change_thread != null {
            dir_change_thread.terminate()
            FindCloseChangeNotification(wait_handle)
        }
        return true
    }

    file_changed :: (ref Self) {
        last_file_change = 0.1
    }

    to_pixel :: (ref Self, wx: double, wy: double) -> (x: int, y: int) {
        return (cast map(wx, xc - xr, xc + xr, 0, double(width)), cast map(wy, yc - yr, yc + yr, double(height), 0))
    }

    to_world :: (ref Self, wx: int, wy: int) -> (x: double, y: double) {
        return (map(double(wx), 0, double(width), xc - xr, xc + xr), map(double(wy), double(height), 0, yc - yr, yc + yr))
    }

    draw_line :: (ref Self, x1: double, y1: double, x2: double, y2: double, col: Pixel) {
        px1, py1 := to_pixel(x1, y1)
        px2, py2 := to_pixel(x2, y2)

        pge.draw_line(px1, py1, px2, py2, col)
    }

    load_function_from_file :: (ref Self) {
        profile_function()
        io.println("reloading...")

        function = None

        match load_file("function.txt") {
            Ok($content) -> {
                var_name_allocator.clear()
                function = compile(content.slice(), var_name_allocator)
            }

            Err($msg) -> {
                io.printfln("Failed to load file: {}", msg)
            }
        }

        match ref function {
            Some($f)    -> {
                io.println("===================")
                for f.slice() {
                    io.println(it)
                }
                io.println("===================")
            }
        }
    }

    on_update :: (ref Self, dt: float) -> bool {
        profile_function()

        {
            // for 0..100000 {}
            profile_scope("clear")
            pge.clear()
        }

        // 
        {
            profile_scope("other stuff")

            last_file_change -= dt
            if last_file_change <= 0 and last_file_change > -1 {
                load_function_from_file()
                last_file_change = -2
            }
            last_file_change = max(last_file_change, -2.0)

            // update dimensions
            width = pge.get_width()
            height = pge.get_height()

            // aspect ratio
            xr = yr * double(width) / double(height)
        }

        handle_input(dt)
        draw_grid()

        mouse_pos := pge.get_mouse_pos()
        variables["mx"] = Complex(double(mouse_pos.x) / double(width))
        variables["my"] = Complex(1 - double(mouse_pos.y) / double(height))

        mouse_pos_w := to_world(mouse_pos.x, mouse_pos.y)
        variables["mxw"] = Complex(mouse_pos_w.x)
        variables["myw"] = Complex(mouse_pos_w.y)

        variables["t"] = <<&variables["t"] + Complex(dt)

        draw_current_function()

        return true
    }

    evaluate_function :: (ref Self, x: double) -> Option[Complex] {
        //return functions[func_index % functions.length](x)
        variables["x"] = Complex(x, 0.0)
        return match ref function {
            Some($f)    -> evaluate(f.slice(), variables)
            None        -> None
        }
    }

    draw_current_function :: (ref Self) {
        profile_function()
        prev := Complex()

        first := true
        for xp: 0..width {
            x := map(double(xp), 0, double(width), xc - xr, xc + xr)

            match evaluate_function(x) {
                Some($c) -> {
                    if first {
                        prev = c
                        first = false
                    }

                    pge.draw_line(xp - 1, cast map(prev.r, yc - yr, yc + yr, double(height), 0), xp, cast map(c.r, yc - yr, yc + yr, double(height), 0), Pixel.rgba(230, 50, 50))
                    pge.draw_line(xp - 1, cast map(prev.i, yc - yr, yc + yr, double(height), 0), xp, cast map(c.i, yc - yr, yc + yr, double(height), 0), Pixel.rgba(50, 230, 50))

                    prev = c
                }

                None -> {
                    first = true
                }
            }
        }

        mouse_x       := pge.get_mouse_pos().x
        mouse_x_world := to_world(mouse_x, 0).x
        match evaluate_function(mouse_x_world) {
            Some($c) -> {
                pcr := to_pixel(0, c.r).y
                pci := to_pixel(0, c.i).y

                pge.draw_circle(mouse_x, pcr, 5, Pixel.rgba(255, 0, 0))
                pge.draw_circle(mouse_x, pci, 5, Pixel.rgba(0, 255, 0))
            }
        }
    }

    draw_grid :: (ref Self) {
        profile_function()
        // grid
        {
            lw   := 0.15
            wmin := to_world(0, height)
            wmax := to_world(width, 0)

            dist  := 1 / (2 * xr) * double(width)
            scale := 1.0
            while dist < 10 {
                dist *= 10
                scale *= 10
            }

            lw *= scale

            // vertical
            for(inclusive=true) _x: 0..int(wmax.x / scale) {
                brightness : u8 = if _x == 0 then 200
                    else if _x % 10 == 0 then 150
                    else if _x % 5 == 0 then 100
                    else 50

                x := double(_x) * scale
                draw_line(x, wmin.y, x, wmax.y, Pixel.rgba(brightness, brightness, brightness))
                draw_line(x, -lw, x, lw, Pixel.rgba(200, 200, 200))
            }

            for _x: int(wmin.x / scale)..0 {
                brightness : u8 = if _x == 0 then 200
                    else if _x % 10 == 0 then 150
                    else if _x % 5 == 0 then 100
                    else 50

                x := double(_x) * scale
                draw_line(x, wmin.y, x, wmax.y, Pixel.rgba(brightness, brightness, brightness))
                draw_line(x, -lw, x, lw, Pixel.rgba(200, 200, 200))
            }

            // horizontal
            for(inclusive=true) _y: 0..int(wmax.y / scale) {
                brightness : u8 = if _y == 0 then 200
                    else if _y % 10 == 0 then 150
                    else if _y % 5 == 0 then 100
                    else 50

                y := double(_y) * scale
                draw_line(wmin.x, y, wmax.x, y, Pixel.rgba(brightness, brightness, brightness))
                draw_line(-lw, y, lw, y, Pixel.rgba(200, 200, 200))
            }

            for _y: int(wmin.y / scale)..0 {
                brightness : u8 = if _y == 0 then 200
                    else if _y % 10 == 0 then 150
                    else if _y % 5 == 0 then 100
                    else 50

                y := double(_y) * scale
                draw_line(wmin.x, y, wmax.x, y, Pixel.rgba(brightness, brightness, brightness))
                draw_line(-lw, y, lw, y, Pixel.rgba(200, 200, 200))
            }
        }
    }

    handle_input :: (ref Self, dt: double) {
        profile_function()
        scroll_speed := 0.001

        // move
        {
            mouse_pos       := pge.get_mouse_pos()
            mouse_pos_world := to_world(mouse_pos.x, mouse_pos.y)
            mouse_delta     : (x: double, y: double) = (mouse_pos_world.x - last_mouse_pos.x, mouse_pos_world.y - last_mouse_pos.y)

            if pge.get_mouse(0).held {
                xc -= mouse_delta.x
                yc -= mouse_delta.y
            }
            last_mouse_pos = to_world(mouse_pos.x, mouse_pos.y)
        }

        // zoom
        {
            mouse_pos       := pge.get_mouse_pos()
            mouse_pos_world := to_world(mouse_pos.x, mouse_pos.y)

            yr *= 1 - double(pge.get_mouse_wheel()) * scroll_speed
            yr = fmax(yr, 0.1)
            // aspect ratio
            xr = yr * double(width) / double(height)

            mouse_pos_world2 := to_world(mouse_pos.x, mouse_pos.y)
            mouse_delta      : (x: double, y: double) = (mouse_pos_world2.x - mouse_pos_world.x, mouse_pos_world2.y - mouse_pos_world.y)
            xc -= mouse_delta.x
            yc -= mouse_delta.y
        }

        // cycle functions
        if pge.get_key(Key.Space).pressed {
            func_index += 1
        }

        if pge.get_key(Key.F5).pressed {
            load_function_from_file()
        }

        if pge.get_mouse(2).pressed {
            mouse_x_world := to_world(pge.get_mouse_pos().x, 0).x
            match evaluate_function(mouse_x_world) {
                Some($c) -> {
                    io.printfln("f({}) = {}", (mouse_x_world, c))
                }

                None -> io.printfln("f({}) is undefined", mouse_x_world)
            }
        }
    }
}

compare_strings :: (a: string, b: string) -> bool {
    return streq(a, b)
}

Main :: () {
    Instrumentor.init(false)
    Instrumentor.get().begin_session("graphyfier", "graphyfier_profile.json")
    myGame := MyGame(
        var_name_allocator = ArenaAllocator.create(1024)
        variables = Variables.create(compare_strings)
    )
    pge := PixelGameEngine.create(myGame, width, height)
    if pge != null {
        pge.start()
    } else {
        io.println("failed to create pixel game engine")
    }
    Instrumentor.get().end_session()
}

// */

profile_scope :: (name: string) #macro #transparent {
    if const enable_profiler {
        _ := Timer.start(name)
    } else {
        _ := 0
    }
}

profile_function :: () #macro #transparent {
    if const enable_profiler {
        _ := Timer.start(@function_signature())
    } else {
        _ := 0
    }
}

profile_async :: (name: string, id: int, ph: string) #macro {
    if const enable_profiler {
        Instrumentor.get().
            write_profile_async(name, id, get_time_microseconds(), ph, Thread.current().id)
    }
}