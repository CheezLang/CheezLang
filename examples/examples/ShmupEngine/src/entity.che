use import std.array
use import std.string
use import std.rc
mem :: import std.mem.allocator
io  :: import std.io
fmt :: import std.fmt

use import serpa.serpa
use import compiler.lexer
use import compiler.error_handler

use import shmup_engine
use import math
use import layer
use import input
use import renderer
use import components

#export_scope

Entity :: struct {
    id          : int
    name        : String
    dead        := false
    active      := true
    position    := vec3(0, 0, 0)
    size        := vec3(1, 1, 1)
    angle       := 0.0f
    components  := Array[Rc[Component]].create()
}

impl Entity {
    init :: (&Self) {
        for &components {
            it.get().init()
        }
    }

    update :: (&Self, layer: &EntityLayer, dt: float) {
        for &components {
            it.get().update(self, layer, dt)
        }
    }

    render :: (&Self, engine: &ShmupEngine, renderer: &Renderer) {
        for &components {
            it.get().render(self, engine, renderer)
        }
    }

    add_component :: (&Self, component: Rc[Component]) {
        component.get().base().entity = ^self
        components.add(component)
    }

    get_component :: (&Self, $T: type) -> Option[Rc[T]] {
        for &components {
            match &it.get() {
                T($comp) -> {
                    return Some(it.clone().change_type(T))
                }
            }
        }

        return None
    }
}

impl IParser[Rc[Entity]] for Rc[Entity] {
    parse :: (parser: &Parser, lexer: &Lexer, error_handler: ^ErrorHandler) -> Option[Self] {
        return match parser.parse(Entity, lexer, error_handler) {
            Some($i) -> Some(Rc[Entity].new(i))
            None -> None
        }
    }
}