use import std.string
use import std.array
use import std.math
use import std.time
use import std.rc
use import std.mem.std_heap_allocator
mem :: import std.mem.allocator
fmt :: import std.fmt
io  :: import std.io
fs  :: import std.io.fs
C   :: import std.c

use import glfw.glfw3
use import opengl.glad_binding
ImGui :: import imgui.imgui_binding

use import opengl_utils
use import matrix
use import renderer
use import math
use import shmup_engine

#export_scope

GameView :: trait {
    initialize      :: (ref Self, max_size: vec2i) -> bool;
    update          :: (ref Self, is_playing: bool, world_size: vec3);
    begin_render    :: (ref Self, is_playing: bool, world_size: vec3, renderer: ref Renderer) -> bool;
    end_render      :: (ref Self);
    begin_gizmos    :: (ref Self);
    end_gizmos      :: (ref Self);
}

WindowBase :: struct #extendable {
    engine      : &ShmupEngine  = null
    viewport    : Viewport      = default
    clear_color : vec4          = default
}

impl GameWindow {

}

GameWindow :: struct #extend(WindowBase) {
}

impl GameView for GameWindow {
    initialize :: (ref Self, max_size: vec2i) -> bool {
        viewport.set_scale(1)
        viewport.set_translation(vec3())
        return viewport.initialize(max_size)
    }

    update :: (ref Self, is_playing: bool, world_size: vec3) {
        // set window properties of viewport
        ImGui.Begin("Game", null, @bin_or(
            ImGui.ImGuiWindowFlags_.NoScrollbar,
            ImGui.ImGuiWindowFlags_.NoScrollWithMouse,
            ImGui.ImGuiWindowFlags_.NoCollapse
        ))

        { // calculate focused state
            was_focused := viewport.is_focused
            viewport.is_focused = ImGui.IsWindowFocused()
            viewport.lost_focused = was_focused and !viewport.is_focused
            viewport.is_hovered = ImGui.IsWindowHovered()
            // @todo: send focus event to layer_stack
        }

        { // get size of viewport window
            min           := ImGui.GetWindowContentRegionMin()
            max           := ImGui.GetWindowContentRegionMax()
            size          := ImGui.ImVec2(max.x - min.x, max.y - min.y)
            viewport_size := vec2i(int(size.x), int(size.y)).clamped(0, int.max)

            window_min := ImGui.GetWindowPos()

            viewport.pos  = vec3(window_min.x + min.x, window_min.y + min.y, 0)
            viewport.size = vec3(cast viewport_size.x, cast viewport_size.y, 1)
            // io.println(fmt.format("+ {}", [viewport.size]))
        }

        ImGui.End()

        viewport.update_projection_matrix(world_size)
        viewport.set_scale(1)
        viewport.set_translation(vec3())

        if viewport.is_focused {
            engine.game_viewport = &self.viewport
        }
    }

    begin_render :: (ref Self, is_playing: bool, world_size: vec3, renderer: ref Renderer) -> bool {
        // draw viewport window
        ImGui.Begin("Game")
        if viewport.size.x > 0 and viewport.size.y > 0 {
            size    := ImGui.ImVec2(viewport.size.x, viewport.size.y)
            uv0     := ImGui.ImVec2(0, size.y / viewport.size_max.y)
            uv1     := ImGui.ImVec2(size.x / viewport.size_max.x, 0)
            tint    := ImGui.ImVec4(1, 1, 1, 1)
            border  := ImGui.ImVec4(0, 0, 0, 0)
            ImGui.Image(cast viewport.render_texture, size, uv0, uv1, tint, border)
        }
        ImGui.End()

        // bind viewport
        viewport.bind()

        // set aspect_ratio_axis so that the game world always fits on screen
        viewport_ar := viewport.size.x / viewport.size.y
        world_ar    := world_size.x / world_size.y
        if viewport_ar > world_ar {
            viewport.aspect_ratio_axis = 1
        } else {
            viewport.aspect_ratio_axis = 0
        }

        // render mode
        renderer.begin_scene(viewport.projection_matrix)

        return false
    }

    end_render :: (ref Self) {
        glBindFramebuffer(GL_FRAMEBUFFER, 0)
        // renderer.end_scene()
    }

    begin_gizmos    :: (ref Self) {
    }
    end_gizmos      :: (ref Self) {
    }
}

EditorWindow :: struct #extend(WindowBase) {
    camera_pos  := vec3()
    zoom        := 1.0f

    last_mouse_pos_s := vec3()
}

impl GameView for EditorWindow {
    initialize :: (ref Self, max_size: vec2i) -> bool {
        viewport.set_scale(1)
        viewport.set_translation(vec3())
        return viewport.initialize(max_size)
    }

    update :: (ref Self, is_playing: bool, world_size: vec3) {
        ImGui.Begin("Scene", null, @bin_or(
            ImGui.ImGuiWindowFlags_.NoScrollbar,
            ImGui.ImGuiWindowFlags_.NoScrollWithMouse,
            ImGui.ImGuiWindowFlags_.NoCollapse
        ))

        { // calculate focused state
            was_focused := viewport.is_focused
            viewport.is_focused = ImGui.IsWindowFocused()
            viewport.lost_focused = was_focused and !viewport.is_focused
            viewport.is_hovered = ImGui.IsWindowHovered()
            // @todo: send focus event to layer_stack
        }

        { // get size of viewport window
            min           := ImGui.GetWindowContentRegionMin()
            max           := ImGui.GetWindowContentRegionMax()
            size          := ImGui.ImVec2(max.x - min.x, max.y - min.y)
            viewport_size := vec2i(int(size.x), int(size.y)).clamped(0, int.max)

            window_min := ImGui.GetWindowPos()

            viewport.pos  = vec3(window_min.x + min.x, window_min.y + min.y, 0)
            viewport.size = vec3(cast viewport_size.x, cast viewport_size.y, 1)
        }

        ImGui.End()

        viewport.update_projection_matrix(world_size)

        // update projection matrices
        if viewport.is_hovered {
            mouse_pos_s := {
                im_pos := ImGui.GetMousePos()
                vec3(im_pos.x, im_pos.y, 0)
            }
            mouse_pos_w      := viewport.screen_to_world(mouse_pos_s)
            last_mouse_pos_w := viewport.screen_to_world(last_mouse_pos_s)
            last_mouse_pos_s = mouse_pos_s

            { // move with mouse
                if ImGui.IsMouseDown(ImGui.ImGuiMouseButton_.Middle) {
                    camera_pos -= mouse_pos_w - last_mouse_pos_w
                }
                viewport.set_translation(-camera_pos)
                last_mouse_pos_w = viewport.screen_to_world(mouse_pos_s)
            }

            { // zoom
                mouse_scroll := int(ImGui.GetIO().MouseWheel)
                while mouse_scroll > 0, mouse_scroll -= 1 {
                    zoom *= 1.1f
                }
                while mouse_scroll < 0, mouse_scroll += 1 {
                    zoom /= 1.1f
                }
                if zoom < 0.1f {
                    zoom = 0.1f
                }
                viewport.set_scale(zoom)
            }

            { // move camera after zoom so we zoom to the cursor
                mouse_pos_w = viewport.screen_to_world(mouse_pos_s)
                camera_pos -= mouse_pos_w - last_mouse_pos_w
                viewport.set_translation(-camera_pos)
            }
        }
    }

    begin_render :: (ref Self, is_playing: bool, world_size: vec3, renderer: ref Renderer) -> bool {
        // draw viewport window
        ImGui.Begin("Scene")
        if viewport.size.x > 0 and viewport.size.y > 0 {
            size    := ImGui.ImVec2(viewport.size.x, viewport.size.y)
            uv0     := ImGui.ImVec2(0, size.y / viewport.size_max.y)
            uv1     := ImGui.ImVec2(size.x / viewport.size_max.x, 0)
            tint    := ImGui.ImVec4(1, 1, 1, 1)
            border  := ImGui.ImVec4(0, 0, 0, 0)
            ImGui.Image(cast viewport.render_texture, size, uv0, uv1, tint, border)
        }
        ImGui.End()

        // bind viewport
        viewport.bind()
        engine.editor_viewport = &self.viewport

        // render mode
        view_projection := viewport.projection_matrix * viewport.view_matrix
        renderer.begin_scene(view_projection)

        return true
    }

    end_render :: (ref Self) {
        glBindFramebuffer(GL_FRAMEBUFFER, 0)
        // renderer.end_scene()
    }

    begin_gizmos    :: (ref Self) {
        ImGui.Begin("Scene")
    }
    end_gizmos      :: (ref Self) {
        ImGui.End()
    }
}

GameAndEditorWindow :: struct #extend(WindowBase) {
    camera_pos  := vec3()
    zoom        := 1.0f

    last_mouse_pos_s := vec3()
}

impl GameView for GameAndEditorWindow {
    initialize :: (ref Self, max_size: vec2i) -> bool {
        viewport.set_scale(1)
        viewport.set_translation(vec3())
        return viewport.initialize(max_size)
    }

    update :: (ref Self, is_playing: bool, world_size: vec3) {
        ImGui.Begin("GameScene", null, @bin_or(
            ImGui.ImGuiWindowFlags_.NoScrollbar,
            ImGui.ImGuiWindowFlags_.NoScrollWithMouse,
            ImGui.ImGuiWindowFlags_.NoCollapse
        ))

        { // calculate focused state
            was_focused := viewport.is_focused
            viewport.is_focused = ImGui.IsWindowFocused()
            viewport.lost_focused = was_focused and !viewport.is_focused
            viewport.is_hovered = ImGui.IsWindowHovered()
            // @todo: send focus event to layer_stack
        }

        { // get size of viewport window
            min           := ImGui.GetWindowContentRegionMin()
            max           := ImGui.GetWindowContentRegionMax()
            size          := ImGui.ImVec2(max.x - min.x, max.y - min.y)
            viewport_size := vec2i(int(size.x), int(size.y)).clamped(0, int.max)

            window_min := ImGui.GetWindowPos()

            viewport.pos  = vec3(window_min.x + min.x, window_min.y + min.y, 0)
            viewport.size = vec3(cast viewport_size.x, cast viewport_size.y, 1)
            // io.println(fmt.format("+ {}", [viewport.size]))
        }

        ImGui.End()

        viewport.update_projection_matrix(world_size)

        // movement
        if is_playing {
            viewport.set_translation(vec3())
            viewport.set_scale(1.0f)
        } else if viewport.is_hovered {
            viewport.set_scale(zoom)
            viewport.set_translation(-camera_pos)

            mouse_pos_s := {
                im_pos := ImGui.GetMousePos()
                vec3(im_pos.x, im_pos.y, 0)
            }
            mouse_pos_w      := viewport.screen_to_world(mouse_pos_s)
            last_mouse_pos_w := viewport.screen_to_world(last_mouse_pos_s)
            last_mouse_pos_s = mouse_pos_s

            { // move with mouse
                if ImGui.IsMouseDown(ImGui.ImGuiMouseButton_.Middle) {
                    camera_pos -= mouse_pos_w - last_mouse_pos_w
                }
                viewport.set_translation(-camera_pos)
                last_mouse_pos_w = viewport.screen_to_world(mouse_pos_s)
            }

            { // zoom
                mouse_scroll := int(ImGui.GetIO().MouseWheel)
                while mouse_scroll > 0, mouse_scroll -= 1 {
                    zoom *= 1.1f
                }
                while mouse_scroll < 0, mouse_scroll += 1 {
                    zoom /= 1.1f
                }
                if zoom < 0.1f {
                    zoom = 0.1f
                }
                viewport.set_scale(zoom)
            }

            { // move camera after zoom so we zoom to the cursor
                mouse_pos_w = viewport.screen_to_world(mouse_pos_s)
                camera_pos -= mouse_pos_w - last_mouse_pos_w
                viewport.set_translation(-camera_pos)
            }
        } else {
            viewport.set_scale(zoom)
            viewport.set_translation(-camera_pos)
        }

        if viewport.is_focused {
            engine.game_viewport = &self.viewport
        }
    }

    begin_render :: (ref Self, is_playing: bool, world_size: vec3, renderer: ref Renderer) -> bool {
        // render game
        size := viewport.size.change_type(i32)
        glBindFramebuffer(GL_FRAMEBUFFER, viewport.frame_buffer)
        glViewport(0, 0, size.x, size.y)

        glClearColor(clear_color.x, clear_color.y, clear_color.z, clear_color.w)
        glClear(GL_COLOR_BUFFER_BIT)

        // set aspect_ratio_axis so that the game world always fits on screen
        if is_playing {
            viewport_ar := viewport.size.x / viewport.size.y
            world_ar    := world_size.x / world_size.y
            if viewport_ar > world_ar {
                viewport.aspect_ratio_axis = 1
            } else {
                viewport.aspect_ratio_axis = 0
            }
        } else {
            viewport.aspect_ratio_axis = 1.0f
        }

        engine.editor_viewport = &self.viewport

        // render mode
        view_projection := viewport.projection_matrix * viewport.view_matrix
        renderer.begin_scene(view_projection)

        return (!is_playing)
    }

    end_render :: (ref Self) {
        glBindFramebuffer(GL_FRAMEBUFFER, 0)
        // renderer.end_scene()

        // draw viewport window
        ImGui.Begin("GameScene")
        if viewport.size.x > 0 and viewport.size.y > 0 {
            size    := ImGui.ImVec2(viewport.size.x, viewport.size.y)
            uv0     := ImGui.ImVec2(0, size.y / viewport.size_max.y)
            uv1     := ImGui.ImVec2(size.x / viewport.size_max.x, 0)
            tint    := ImGui.ImVec4(1, 1, 1, 1)
            border  := ImGui.ImVec4(0, 0, 0, 0)
            ImGui.Image(cast viewport.render_texture, size, uv0, uv1, tint, border)
        }
        ImGui.End()
    }

    begin_gizmos    :: (ref Self) {
        ImGui.Begin("GameScene")
    }
    end_gizmos      :: (ref Self) {
        ImGui.End()
    }
}

Viewport :: struct {
    pos                     := vec3()
    size                    := vec3()
    size_max                := vec3()

    clear_color             := vec4()
    aspect_ratio_axis       := 1.0f

    translation_matrix      := mat4()
    translation_matrix_inv  := mat4()
    scale_matrix            := mat4()
    scale_matrix_inv        := mat4()
    view_matrix             := mat4()
    projection_matrix       := mat4()
    projection_inverse      := mat4()
    view_projection_inv     := mat4()

    // 
    frame_buffer   : u32 = 0
    render_texture : u32 = 0
    depth_texture  : u32 = 0
    is_focused     := false
    lost_focused   := false
    is_hovered     := false
}

impl Viewport {
    initialize :: (ref Self, max_size: vec2i) -> bool {
        max_size := max_size.change_type(i32)
        self.size_max = vec3(float(max_size.x), float(max_size.y), 1)
        texture_width := i32(max_size.x)
        texture_height := i32(max_size.y)

        // The framebuffer, which regroups 0, 1, or more textures, and 0 or 1 depth buffer.
        (glGenFramebuffers(1, &frame_buffer))
        (glBindFramebuffer(GL_FRAMEBUFFER, frame_buffer))

        // The texture we're going to render to
        GL(glGenTextures(1, &render_texture))

        // "Bind" the newly created texture : all future texture functions will modify this texture
        GL(glBindTexture(GL_TEXTURE_2D, render_texture))

        // Give an empty image to OpenGL ( the last "null" )
        GL(glTexImage2D(GL_TEXTURE_2D, 0, GL_RGB, texture_width, texture_height, 0, GL_RGB, GL_UNSIGNED_BYTE, null))

        // Poor filtering. Needed !
        GL(glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST))
        GL(glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST))

        // The depth buffer
        GL(glGenRenderbuffers(1, &depth_texture))
        GL(glBindRenderbuffer(GL_RENDERBUFFER, depth_texture))
        GL(glRenderbufferStorage(GL_RENDERBUFFER, GL_DEPTH_COMPONENT, texture_width, texture_height))
        GL(glFramebufferRenderbuffer(GL_FRAMEBUFFER, GL_DEPTH_ATTACHMENT, GL_RENDERBUFFER, depth_texture))

        // Set "render_texture" as our colour attachement #0
        GL(glFramebufferTexture(GL_FRAMEBUFFER, GL_COLOR_ATTACHMENT0, render_texture, 0))

        // Set the list of draw buffers.
        draw_buffers := [u32(GL_COLOR_ATTACHMENT0)]
        GL(glDrawBuffers(1, draw_buffers.data)) // "1" is the size of draw_buffers

        // Always check that our framebuffer is ok
        if glCheckFramebufferStatus(GL_FRAMEBUFFER) != GL_FRAMEBUFFER_COMPLETE {
            return false
        }

        glBindTexture(GL_TEXTURE_2D, 0)
        glBindRenderbuffer(GL_RENDERBUFFER, 0)
        glBindFramebuffer(GL_FRAMEBUFFER, 0)
        return true
    }

    bind :: (ref Self) {
        size := self.size.change_type(i32)
        glBindFramebuffer(GL_FRAMEBUFFER, self.frame_buffer)
        glViewport(0, 0, size.x, size.y)

        glClearColor(clear_color.x, clear_color.y, clear_color.z, clear_color.w)
        glClear(GL_COLOR_BUFFER_BIT)
    }

    set_translation :: (ref Self, offset: vec3) {
        translation_matrix     = mat4.from_translation(offset)
        translation_matrix_inv = mat4.from_translation(-offset)
        view_matrix            = scale_matrix * translation_matrix
        view_projection_inv    = translation_matrix_inv * scale_matrix_inv * projection_inverse
    }

    set_scale :: (ref Self, scale: float) {
        scale_matrix        = mat4.from_scales(scale)
        scale_matrix_inv    = mat4.from_scales(1 / scale)
        view_matrix         = scale_matrix * translation_matrix
        view_projection_inv = translation_matrix_inv * scale_matrix_inv * projection_inverse
    }

    screen_to_local :: (ref Self, pos: vec3) -> vec3 {
        return pos - self.pos
    }

    local_to_clip :: (ref Self, pos: vec3) -> vec3 {
        return (pos / size * 2.0f - vec3(1, 1, 0)) * vec3(1, -1, 0)
    }

    clip_to_world :: (ref Self, pos: vec3) -> vec3 {
        return view_projection_inv * pos
    }

    screen_to_world :: (ref Self, pos: vec3) -> vec3 {
        return view_projection_inv * (((pos - self.pos) / size * 2.0f - vec3(1, 1, 0)) * vec3(1, -1, 0))
    }

    world_to_screen :: (ref Self, pos: vec3) -> vec3 {
        clip_pos := projection_matrix * view_matrix * pos
        ndc_pos := clip_pos * vec3(1, -1) * 0.5f + 0.5f
        screen_pos := ndc_pos * size + self.pos
        return screen_pos
    }

    update_projection_matrix :: (ref Self, world_size: vec3) {
        height1 := world_size.y
        width1  := height1 * size.x / size.y

        width2  := world_size.x
        height2 := width2 * size.y / size.x

        width   := aspect_ratio_axis * width1  + (1 - aspect_ratio_axis) * width2
        height  := aspect_ratio_axis * height1 + (1 - aspect_ratio_axis) * height2

        projection_matrix = mat4.orthographic(-width * 0.5, width * 0.5, -height * 0.5, height * 0.5, -10, 10)
        projection_inverse = projection_matrix.invert_orthographic()
    }
}