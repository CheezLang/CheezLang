#load("std:io/io")
#load("std:math")
#load("std:array")
#load("std:mem/stack_allocator")
#load("bmp:bmp")

///////////////////////////////////////////////////////////
fn xor(a: bool, b: bool) -> bool {
    return (a and !b) or (!a and b)
}

fn mod(a: $T, b: T) -> T {
    return ((a % b) + b) % b
}

///////////////////////////////////////////////////////////

struct Ray {
    origin: Vec3
    direction: Vec3
}

struct DistanceShaderPair {
    distance: f64
    shader: Shader
}

////////////////////////////////// SDF //////////////////////////////////////////////

///////////////////// basic ///////////////////
trait SDF {
    fn distance(v: Vec3) -> DistanceShaderPair;
}

struct Sphere {
    radius: f64
    shader: Shader
}

impl SDF for Sphere {
    fn distance(v: Vec3) -> DistanceShaderPair {
        return new DistanceShaderPair {
            distance = v.mag() - radius
            shader = shader
        }
    }
}

struct PlaneY {
    location: f64
    shader: Shader
}

impl SDF for PlaneY {
    fn distance(v: Vec3) -> DistanceShaderPair {
        return new DistanceShaderPair {
            distance = v.y - location
            shader = shader
        }
    }
}

struct Cube {
    radius: f64
    shader: Shader
}

impl SDF for Cube {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let dx = fabs(v.x) - radius
        let dy = fabs(v.y) - radius
        let dz = fabs(v.z) - radius

        let d: f64 = 0

        if dx > 0 and dy > 0 and dz > 0 { // corner
            let r = new Vec3 {
                dx, dy, dz
            }
            d = r.mag()
        }
        else if dx > 0 and dy > 0 {
            let r = new Vec3 {
                x = dx
                y = dy
                z = 0
            }
            d = r.mag()
        }
        else if dx > 0 and dz > 0 {
            let r = new Vec3 {
                x = dx
                y = 0
                z = dz
            }
            d = r.mag()
        }
        else if dy > 0 and dz > 0 {
            let r = new Vec3 {
                x = 0
                y = dy
                z = dz
            }
            d = r.mag()
        }
        else {
            d = fmax(dx, fmax(dy, dz))
        }

        return new DistanceShaderPair {
            distance = d
            shader = shader
        }
    }
}

///////////////////// transformations //////////////////////////

struct TranslatedSDF {
    sdf: SDF
    translation: Vec3
}

impl SDF for TranslatedSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        return sdf.distance(v.sub(translation))
    }
}

struct RotatedXSDF {
    sdf: SDF
    angle: f64
}

struct RotatedYSDF {
    sdf: SDF
    angle: f64
}

struct RotatedZSDF {
    sdf: SDF
    angle: f64
}

impl SDF for RotatedXSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let rad = angle / 360 * 2 * 3.1415
        let c = cos(rad)
        let s = sin(rad)
        let y = c * v.y - s * v.z
        let z = s * v.y + c * v.z
        return sdf.distance(new Vec3{v.x, y, z})
    }
}

impl SDF for RotatedYSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let rad = angle / 360 * 2 * 3.1415
        let c = cos(rad)
        let s = sin(rad)
        let x = c * v.x + s * v.z
        let z = -s * v.x + c * v.z
        return sdf.distance(new Vec3{x, v.y, z})
    }
}

impl SDF for RotatedZSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let rad = angle / 360 * 2 * 3.1415
        let c = cos(rad)
        let s = sin(rad)
        let x = c * v.x - s * v.y
        let y = s * v.x + c * v.y
        return sdf.distance(new Vec3{x, y, v.z})
    }
}


////////////////////// combinations ///////////////////////
struct UnionSDF {
    sdf1: SDF
    sdf2: SDF
}

impl SDF for UnionSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let a = sdf1.distance(v)
        let b = sdf2.distance(v)
        if a.distance < b.distance {
            return a
        } else {
            return b
        }
    }
}

struct IntersectionSDF {
    sdf1: SDF
    sdf2: SDF
}

impl SDF for IntersectionSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let a = sdf1.distance(v)
        let b = sdf2.distance(v)
        if a.distance > b.distance {
            return a
        } else {
            return b
        }
    }
}

struct SubtractionSDF {
    sdf1: SDF
    sdf2: SDF
}

impl SDF for SubtractionSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let a = sdf1.distance(v)
        let b = sdf2.distance(v)

        b.distance = -b.distance
        if a.distance > b.distance {
            return a
        } else {
            return b
        }
    }
}

////////////////////////////// shaders //////////////////////////////////

trait Shader {
    fn shade(ray: Ray, raymarcher: *Raymarcher) -> Vec3;
}

//////
struct ConstantColorShader {
    color: Vec3
}

impl Shader for ConstantColorShader {
    fn shade(ray: Ray, raymarcher: *Raymarcher) -> Vec3 {
        return color
    }
}

//////
struct LambertianShader {
    color: Vec3
}

impl Shader for LambertianShader {
    fn shade(ray: Ray, raymarcher: *Raymarcher) -> Vec3 {
        let ld: Vec3
        let lc: Vec3
        raymarcher.sampleLightDirection(&ld, &lc)
        let normal = raymarcher.normal(ray.origin)

        let i = normal.dot(ld)
        let c = color.mul3(lc)

        let sdfHit = ray.origin.add(ld.mul(0.001))
        let shadowRay = new Ray{sdfHit, ld}
        let distance = raymarcher.sdf.distance(shadowRay.origin).distance

        while shadowRay.origin.mag() < raymarcher.backgroundDistance and fabs(distance) > 0.00005 {
            shadowRay.origin = shadowRay.origin.add(shadowRay.direction.mul(distance))
            distance = raymarcher.sdf.distance(shadowRay.origin).distance
        }

        let shadow: f64 = 1.0
        if shadowRay.origin.mag() < raymarcher.backgroundDistance {
            shadow = 0.3
        }
        
        let c = c.mul(fmax(i, 0.1))
        return c.mul(shadow)
    }
}

struct CheckerBoardShader {
    size: f64
    shader1: Shader
    shader2: Shader
}


impl Shader for CheckerBoardShader {
    fn shade(ray: Ray, raymarcher: *Raymarcher) -> Vec3 {
        let x = mod(ray.origin.x, 2 * size)
        let y = mod(ray.origin.y, 2 * size)
        let z = mod(ray.origin.z, 2 * size)

        if xor(x < size, xor(y < size, z < size)) {
            return shader1.shade(ray, raymarcher)
        } else {
            return shader2.shade(ray, raymarcher)
        }
    }
}

///////////////////////////// scene //////////////////////////////
struct Background {

}

impl Background {
    fn project(ray: Ray) -> Vec3 {
        let i = ray.direction.y + 0.5
        i *= i
        return new Vec3 { i, i, i }
    }
}

struct Raymarcher {
    sdf: SDF
    background: Background
    backgroundDistance: f64 = 1000
}

impl Raymarcher {
    ref fn project(ray: Ray) -> Vec3 {
        let ds = sdf.distance(ray.origin)

        while fabs(ds.distance) > 0.0000001 {
            ray.origin = ray.origin.add(ray.direction.mul(ds.distance))
            ds = sdf.distance(ray.origin)


            if ray.origin.mag() >= backgroundDistance {
                return background.project(ray)
            }
        }

        return ds.shader.shade(ray, &self)
    }

    ref fn sampleLightDirection(dir: *Vec3, col: *Vec3) {
        <<dir = new Vec3 {
            -1, 1.5, 1
        }.normalized()
        <<col = new Vec3 {1, 1, 1}
    }

    ref fn normal(p: Vec3) -> Vec3 {
        let off: f64 = 0.001

        let n = new Vec3 {
            x = sdf.distance(new Vec3{p.x + off, p.y, p.z}).distance - sdf.distance(new Vec3{p.x - off, p.y, p.z}).distance
            y = sdf.distance(new Vec3{p.x, p.y + off, p.z}).distance - sdf.distance(new Vec3{p.x, p.y - off, p.z}).distance
            z = sdf.distance(new Vec3{p.x, p.y, p.z + off}).distance - sdf.distance(new Vec3{p.x, p.y, p.z - off}).distance
        }
        return n.normalized()
    }
}


/////////////////////////////////////////////////


struct PerspectiveCamera {
    focalLength: f64
    sensorWidth: f64
    sensorHeight: f64
}

impl PerspectiveCamera {
    ref fn project(x: f64, y: f64) -> Ray {
        return new Ray {
            origin = new Vec3 {0, 0, 0}
            direction = new Vec3 {
                x = (x - 0.5) * sensorWidth
                y = -(y - 0.5) * sensorHeight
                z = -focalLength
            }.normalized()
        }
    }
}

struct Film {
    width: u32
    height: u32
    pixels: []Vec3

    antiAliasingSteps: u32
}

impl Film {
    ref fn trigger(camera: PerspectiveCamera, scene: Raymarcher) {

        print_f("({}, {})`n", [width, height])

        let fw = 1 / cast(f64) width
        let fh = 1 / cast(f64) height

        print_f("Rendering image with {} anti-aliasing step(s)`n", [antiAliasingSteps])
        if antiAliasingSteps < 1 {
            antiAliasingSteps = 1
        }
        

        let r = new LinearCongruentialGenerator{
            getCurrentTimeInMicroseconds()
        }

        while let y: u32 = 0; y < height; y += 1 {
            if antiAliasingSteps >= 0 {
                print_f("Rendering row {}/{}`n", [(y + 1), height])
            }

            while let x: u32 = 0; x < width; x += 1 {

                let fx = cast(f64) x
                let fy = cast(f64) y

                let color = new Vec3{0, 0, 0}

                if antiAliasingSteps == 1 {
                    let ray = camera.project((fx + 0.5) * fw, (fy + 0.5) * fh)
                    color= scene.project(ray)
                }
                else {
                    while let i: u32 = 0; i < antiAliasingSteps; i += 1 {
                        let ray = camera.project((fx + r.nextDouble()) * fw, (fy + r.nextDouble()) * fh)
                        let c = scene.project(ray)
                        color = color.add(c)
                    }
                    color = color.mul(1 / cast(f64) antiAliasingSteps)
                }

                pixels[x + y * width] = color
            }
        }
    }
}

///////////////////////////////////////////////////////////////////////////

struct SDFAllocator {
    allocator: StackAllocator
}

impl SDFAllocator {
    ref fn init() {
        allocator = newStackAllocator(1024 * 2)
        allocator.printDiagnostics()
    }

    ref fn dispose() {
        allocator.dispose()
    }

    ref fn allocate() -> *$T {
        let t = cast(*T) allocator.allocate(1, @sizeof(T), @alignof(T))
        print_f("{}`n", [allocator])
        return t
    }

    ref fn sphere(radius: f64, shader: Shader) -> SDF {
        let s: *Sphere = self.allocate()
        s.radius = radius
        s.shader = shader
        return cast(SDF) s
    }

    ref fn sphereT(position: Vec3, radius: f64, shader: Shader) -> SDF {
        return self.translate(self.sphere(radius, shader), position)
    }

    ref fn planeY(location: f64, shader: Shader) -> SDF {
        let s: *PlaneY = self.allocate()
        s.location = location
        s.shader = shader
        return cast(SDF) s
    }

    ref fn cube(radius: f64, shader: Shader) -> SDF {
        let s: *Cube = self.allocate()
        s.radius = radius
        s.shader = shader
        return cast(SDF) s
    }

    ref fn cubeT(position: Vec3, radius: f64, shader: Shader) -> SDF {
        return self.translate(self.cube(radius, shader), position)
    }

    ref fn translate(sdf: SDF, translation: Vec3) -> SDF {
        let t: *TranslatedSDF = self.allocate()
        t.sdf = sdf
        t.translation = translation
        return cast(SDF) t
    }

    ref fn rotateX(sdf: SDF, angle: f64) -> SDF {
        let t: *RotatedXSDF = self.allocate()
        t.sdf = sdf
        t.angle = angle
        return cast(SDF) t
    }

    ref fn rotateY(sdf: SDF, angle: f64) -> SDF {
        let t: *RotatedYSDF = self.allocate()
        t.sdf = sdf
        t.angle = angle
        return cast(SDF) t
    }

    ref fn rotateZ(sdf: SDF, angle: f64) -> SDF {
        let t: *RotatedZSDF = self.allocate()
        t.sdf = sdf
        t.angle = angle
        return cast(SDF) t
    }

    ref fn add(sdf1: SDF, sdf2: SDF) -> SDF {
        let t: *UnionSDF = self.allocate()
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return cast(SDF) t
    }

    ref fn intersect(sdf1: SDF, sdf2: SDF) -> SDF {
        let t: *IntersectionSDF = self.allocate()
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return cast(SDF) t
    }

    ref fn subtract(sdf1: SDF, sdf2: SDF) -> SDF {
        let t: *SubtractionSDF = self.allocate()
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return cast(SDF) t
    }

    ref fn combine(sdf1: SDF, sdf2: SDF) -> *$T {
        let t: *T = self.allocate()
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return cast(SDF) t
    }
}

fn renderImage(film: *Film)
{
    while let i = 0; i < film.pixels.length; i += 1 {
        film.pixels[i] = new Vec3 { 0, 0, 0 }
    }

    let as = cast(f64) film.width / cast(f64) film.height
    let camera = new PerspectiveCamera {
        focalLength = 50
        sensorWidth = as * 24
        sensorHeight = 24
    }

    let bg = new Background{}

    let all: SDFAllocator
    all.init()
    defer all.dispose()

    //
    let sChecker1    = new LambertianShader{new Vec3{0.9, 0.8, 0.8}}
    let sChecker2    = new LambertianShader{new Vec3{0.2, 0.1, 0.1}}
    let sChecker  = new CheckerBoardShader{1, sChecker1, sChecker2}

    let sWhite    = new LambertianShader{new Vec3{1, 1, 1}}
    let sBlack    = new LambertianShader{new Vec3{0, 0, 0}}
    let sRed    = new LambertianShader{new Vec3{1, 1, 1}}
    let sGreen  = new LambertianShader{new Vec3{0, 1, 0}}
    let sBlue   = new LambertianShader{new Vec3{0, 0, 1}}

    //
    let ground = all.planeY(-1.25, sWhite)
    let s1 = all.sphereT(new Vec3{0, 0.5, 0}, 1, sRed)
    let s2 = all.sphereT(new Vec3{0, 0, 0}, 0.3, sRed)
    let c1 = all.cubeT(new Vec3{0, 0.5, 0}, 0.8, sRed)


    // cube
    let sdf: SDF
    {
        sdf = c1
        sdf = all.subtract(sdf, s1)
        // sdf = all.add(sdf, s2)

        sdf = all.rotateX(sdf, -20)
        sdf = all.rotateY(sdf, 20)
    }
    // {
    //     sdf = s1
    //     sdf = all.subtract(sdf, all.sphereT(new Vec3{-1, 0, 1}, 1.2, sWhite))
    // }

    let sdf = all.add(sdf, ground)
    let sdf = all.translate(sdf, new Vec3{0, 0, -7.5})

    let raymarcher = new Raymarcher {
        sdf = sdf
        background = bg
        backgroundDistance = 15
    }

    film.trigger(camera, raymarcher)
}

fn signed_distance_field_renderer()
{
    //
    let scale: u32 = 5
    let width: u32 = 192 * scale
    let height: u32 = 128 * scale
    let antiAliasingSteps: u32 = 1

    let imageArr: Array(Vec3) = newArray()
    defer imageArr.dispose()

    imageArr.resize(width * height)

    let film = new Film { width, height, imageArr.slice(), antiAliasingSteps }

    let start = getCurrentTimeInMilliseconds()
    renderImage(&film)
    let end = getCurrentTimeInMilliseconds()
    let dur = end - start
    let dur = cast(f64) dur / cast(f64) 1000

    print_f("Rendering took {} seconds
", [dur])

    // // store image as bmp
    write_bmp(cast(i32) width, cast(i32) height, cast(*f64) imageArr.data, "test.bmp")
}
