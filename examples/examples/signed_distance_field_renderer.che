#load("../std/io/io.che")
#load("../std/math.che")
#load("../std/array.che")
#load("../std/mem/stack_allocator.che")
#load("../bmp/bmp.che")

///////////////////////////////////////////////////////////

struct Ray {
    origin: Vec3
    direction: Vec3
}

struct DistanceShaderPair {
    distance: double
    shader: Shader
}

////////////////////////////////// SDF //////////////////////////////////////////////

///////////////////// basic ///////////////////
trait SDF {
    fn distance(v: Vec3) -> DistanceShaderPair;
}

struct Sphere {
    radius: double
    shader: Shader
}

impl SDF for Sphere {
    fn distance(v: Vec3) -> DistanceShaderPair {
        return new DistanceShaderPair {
            distance = v.mag() - radius
            shader = shader
        }
    }
}

struct Cube {
    radius: double
    shader: Shader
}

impl SDF for Cube {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let dx = fabs(v.x) - radius
        let dy = fabs(v.y) - radius
        let dz = fabs(v.z) - radius

        let d: double = 0

        if dx > 0 and dy > 0 and dz > 0 { // corner
            let r = new Vec3 {
                dx, dy, dz
            }
            d = r.mag()
        }
        else if dx > 0 and dy > 0 {
            let r = new Vec3 {
                x = dx
                y = dy
                z = 0
            }
            d = r.mag()
        }
        else if dx > 0 and dz > 0 {
            let r = new Vec3 {
                x = dx
                y = 0
                z = dz
            }
            d = r.mag()
        }
        else if dy > 0 and dz > 0 {
            let r = new Vec3 {
                x = 0
                y = dy
                z = dz
            }
            d = r.mag()
        }
        else {
            d = fmax(dx, fmax(dy, dz))
        }

        return new DistanceShaderPair {
            distance = d
            shader = shader
        }
    }
}

///////////////////// transformations //////////////////////////

struct TranslatedSDF {
    sdf: SDF
    translation: Vec3
}

impl SDF for TranslatedSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        return sdf.distance(v.sub(translation))
    }
}

struct RotatedXSDF {
    sdf: SDF
    angle: double
}

struct RotatedYSDF {
    sdf: SDF
    angle: double
}

struct RotatedZSDF {
    sdf: SDF
    angle: double
}

impl SDF for RotatedXSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let rad = angle / 360 * 2 * 3.1415
        let c = cos(rad)
        let s = sin(rad)
        let y = c * v.y - s * v.z
        let z = s * v.y + c * v.z
        return sdf.distance(new Vec3{v.x, y, z})
    }
}

impl SDF for RotatedYSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let rad = angle / 360 * 2 * 3.1415
        let c = cos(rad)
        let s = sin(rad)
        let x = c * v.x + s * v.z
        let z = -s * v.x + c * v.z
        return sdf.distance(new Vec3{x, v.y, z})
    }
}

impl SDF for RotatedZSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let rad = angle / 360 * 2 * 3.1415
        let c = cos(rad)
        let s = sin(rad)
        let x = c * v.x - s * v.y
        let y = s * v.x + c * v.y
        return sdf.distance(new Vec3{x, y, v.z})
    }
}


////////////////////// combinations ///////////////////////
struct UnionSDF {
    sdf1: SDF
    sdf2: SDF
}

impl SDF for UnionSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let a = sdf1.distance(v)
        let b = sdf2.distance(v)
        if a.distance < b.distance {
            return a
        } else {
            return b
        }
    }
}

struct IntersectionSDF {
    sdf1: SDF
    sdf2: SDF
}

impl SDF for IntersectionSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let a = sdf1.distance(v)
        let b = sdf2.distance(v)
        if a.distance > b.distance {
            return a
        } else {
            return b
        }
    }
}

struct SubtractionSDF {
    sdf1: SDF
    sdf2: SDF
}

impl SDF for SubtractionSDF {
    fn distance(v: Vec3) -> DistanceShaderPair {
        let a = sdf1.distance(v)
        let b = sdf2.distance(v)

        b.distance = -b.distance
        if a.distance > b.distance {
            return a
        } else {
            return b
        }
    }
}

////////////////////////////// shaders //////////////////////////////////

trait Shader {
    fn shade(ray: Ray, raymarcher: Raymarcher&) -> Vec3;
}

//////
struct ConstantColorShader {
    color: Vec3
}

impl Shader for ConstantColorShader {
    fn shade(ray: Ray, raymarcher: Raymarcher&) -> Vec3 {
        return color
    }
}

//////
struct LambertianShader {
    color: Vec3
}

impl Shader for LambertianShader {
    fn shade(ray: Ray, raymarcher: Raymarcher&) -> Vec3 {
        let ld: Vec3
        let lc: Vec3
        raymarcher.sampleLightDirection(&ld, &lc)
        let normal = raymarcher.normal(ray.origin)

        let i = normal.dot(ld)
        let c = color.mul3(lc)
        return c.mul(fmax(i, 0.1))
    }
}

///////////////////////////// scene //////////////////////////////
struct Background {

}

impl Background {
    fn project(ray: Ray) -> Vec3 {
        if (ray.direction.y < 0) {
            return new Vec3 { 0.1, 0.4, 0.1 }
        }
        else {
            return new Vec3 { ray.direction.y * 2, ray.direction.y * 4, 1 }
        }
    }
}

struct Raymarcher {
    sdf: SDF
    background: Background
    backgroundDistance: double // = 1000
}

impl Raymarcher {
    ref fn project(ray: Ray) -> Vec3 {
        let ds = sdf.distance(ray.origin)

        while fabs(ds.distance) > 0.0000001 {
            ray.origin = ray.origin.add(ray.direction.mul(ds.distance))
            ds = sdf.distance(ray.origin)


            if ray.origin.mag() >= backgroundDistance {
                return background.project(ray)
            }
        }

        return ds.shader.shade(ray, &self)
    }

    ref fn sampleLightDirection(dir: Vec3&, col: Vec3&) {
        <<dir = new Vec3 {
            -1, 1, 1
        }.normaziled()
        <<col = new Vec3 {1, 1, 1}
    }

    ref fn normal(p: Vec3) -> Vec3 {
        let off: double = 0.001

        let n = new Vec3 {
            x = sdf.distance(new Vec3{p.x + off, p.y, p.z}).distance - sdf.distance(new Vec3{p.x - off, p.y, p.z}).distance
            y = sdf.distance(new Vec3{p.x, p.y + off, p.z}).distance - sdf.distance(new Vec3{p.x, p.y - off, p.z}).distance
            z = sdf.distance(new Vec3{p.x, p.y, p.z + off}).distance - sdf.distance(new Vec3{p.x, p.y, p.z - off}).distance
        }
        return n.normaziled()
    }
}


/////////////////////////////////////////////////


struct PerspectiveCamera {
    focalLength: double
    sensorWidth: double
    sensorHeight: double
}

impl PerspectiveCamera {
    ref fn project(x: double, y: double) -> Ray {
        return new Ray {
            origin = new Vec3 {0, 0, 0}
            direction = new Vec3 {
                x = (x - 0.5) * sensorWidth
                y = -(y - 0.5) * sensorHeight
                z = -focalLength
            }.normaziled()
        }
    }
}

struct Film {
    width: uint
    height: uint
    pixels: Vec3[]
}

impl Film {
    ref fn trigger(camera: PerspectiveCamera, scene: Raymarcher) {

        Printf("({}, {})`n", [width, height])

        let fw = 1 / (double)width
        let fh = 1 / (double)height

        
        while let y: uint = 0; y < height; y += 1 {
            while let x: uint = 0; x < width; x += 1 {

                let fx = (double)x + 0.5
                let fy = (double)y + 0.5
                let ray = camera.project(fx * fw, fy * fh)
                let color = scene.project(ray)

                pixels[x + y * width] = color
            }
        }
    }
}

///////////////////////////////////////////////////////////////////////////

struct SDFAllocator {
    allocator: StackAllocator
}

impl SDFAllocator {
    ref fn Init() {
        allocator = NewStackAllocator(1024 * 2)
        allocator.PrintDiagnostics()
    }

    ref fn Dispose() {
        allocator.Dispose()
    }

    ref fn Allocate() -> $T& {
        let t = (T&)allocator.allocate(1, @sizeof(T), @alignof(T))
        Printf("{}`n", [allocator])
        return t
    }

    ref fn Sphere(radius: double, shader: Shader) -> SDF {
        let s: Sphere& = self.Allocate()
        s.radius = radius
        s.shader = shader
        return (SDF)s
    }

    ref fn SphereT(position: Vec3, radius: double, shader: Shader) -> SDF {
        return self.Translate(self.Sphere(radius, shader), position)
    }

    ref fn Cube(radius: double, shader: Shader) -> SDF {
        let s: Cube& = self.Allocate()
        s.radius = radius
        s.shader = shader
        return (SDF)s
    }

    ref fn CubeT(position: Vec3, radius: double, shader: Shader) -> SDF {
        return self.Translate(self.Cube(radius, shader), position)
    }

    ref fn Translate(sdf: SDF, translation: Vec3) -> SDF {
        let t: TranslatedSDF& = self.Allocate()
        t.sdf = sdf
        t.translation = translation
        return (SDF)t
    }

    ref fn RotateX(sdf: SDF, angle: double) -> SDF {
        let t: RotatedXSDF& = self.Allocate()
        t.sdf = sdf
        t.angle = angle
        return (SDF)t
    }

    ref fn RotateY(sdf: SDF, angle: double) -> SDF {
        let t: RotatedYSDF& = self.Allocate()
        t.sdf = sdf
        t.angle = angle
        return (SDF)t
    }

    ref fn RotateZ(sdf: SDF, angle: double) -> SDF {
        let t: RotatedZSDF& = self.Allocate()
        t.sdf = sdf
        t.angle = angle
        return (SDF)t
    }

    ref fn Add(sdf1: SDF, sdf2: SDF) -> SDF {
        let t: UnionSDF& = self.Allocate()
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return (SDF)t
    }

    ref fn Intersect(sdf1: SDF, sdf2: SDF) -> SDF {
        let t: IntersectionSDF& = self.Allocate()
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return (SDF)t
    }

    ref fn Subtract(sdf1: SDF, sdf2: SDF) -> SDF {
        let t: SubtractionSDF& = self.Allocate()
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return (SDF)t
    }

    ref fn Combine(sdf1: SDF, sdf2: SDF) -> $T& {
        let t: T& = self.Allocate()
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return (SDF)t
    }
}

fn RenderImage(film: Film&)
{
    while let i = 0; i < film.pixels.length; i += 1 {
        film.pixels[i] = new Vec3 { 0, 0, 0 }
    }

    let as = (double)film.width/(double)film.height
    let camera = new PerspectiveCamera {
        focalLength = 50
        sensorWidth = as * 24
        sensorHeight = 24
    }

    let bg = new Background{}

    let all: SDFAllocator
    all.Init()
    defer all.Dispose()

    //
    let sRed    = new LambertianShader{new Vec3{1, 0, 0}}
    let sGreen  = new LambertianShader{new Vec3{0, 1, 0}}
    let sBlue   = new LambertianShader{new Vec3{0, 0, 1}}

    //
    let s1 = all.SphereT(new Vec3{0, 0, 0}, 1, sRed)
    let s2 = all.SphereT(new Vec3{0, 0, 0}, 0.3, sRed)
    let c1 = all.CubeT(new Vec3{0, 0, 0}, 0.8, sRed)

    let sdf = c1
    let sdf = all.Subtract(sdf, s1)
    let sdf = all.Add(sdf, s2)

    let sdf = all.RotateX(sdf, -10)
    let sdf = all.RotateY(sdf, -20)
    let sdf = all.Translate(sdf, new Vec3{0, 0, -6})

    let raymarcher = new Raymarcher {
        sdf = sdf
        background = bg
        backgroundDistance = 100
    }

    film.trigger(camera, raymarcher)
}

fn signed_distance_field_renderer()
{
    //
    let scale: uint = 3
    let width: uint = 360 * scale
    let height: uint = 240 * scale

    let imageArr: Array(Vec3) = NewArray()
    defer imageArr.Dispose()

    imageArr.Resize(width * height)

    let film = new Film { width, height, imageArr.Slice() }

    RenderImage(&film)

    // // store image as bmp
    write_bmp((int)width, (int)height, (double&)imageArr.data, "test.bmp")
}
