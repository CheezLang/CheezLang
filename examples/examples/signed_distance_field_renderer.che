#load("std:io/io")
#load("std:math")
#load("std:array")
#load("std:mem/stack_allocator")
#load("std:random")
#load("bmp:bmp")

Vec3 :: Vector3[double]

///////////////////////////////////////////////////////////
fn xor(a: bool, b: bool) -> bool {
    return (a and !b) or (!a and b)
}

fn mod(a: $T, b: T) -> T {
    return ((a % b) + b) % b
}

///////////////////////////////////////////////////////////

Ray :: struct {
    origin: Vec3
    direction: Vec3
}

DistanceShaderPair :: (distance: double, shader: Shader)

////////////////////////////////// SDF //////////////////////////////////////////////

///////////////////// basic ///////////////////
trait SDF {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair;
}

Sphere :: struct {
    radius: double
    shader: Shader
}

impl SDF for Sphere {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        return (v.mag() - radius, shader)
    }
}

PlaneY :: struct {
    location: double
    shader: Shader
}

impl SDF for PlaneY {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        return (v.y - location, shader)
    }
}

Cube :: struct {
    radius: double
    shader: Shader
}

impl SDF for Cube {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        dx := fabs(v.x) - radius
        dy := fabs(v.y) - radius
        dz := fabs(v.z) - radius

        d : double = 0

        if dx > 0 and dy > 0 and dz > 0 { // corner
            r := Vec3(
                dx, dy, dz
            )
            d = r.mag()
        } else if dx > 0 and dy > 0 {
            r := Vec3(
                x = dx
                y = dy
                z = 0
            )
            d = r.mag()
        } else if dx > 0 and dz > 0 {
            r := Vec3(
                x = dx
                y = 0
                z = dz
            )
            d = r.mag()
        } else if dy > 0 and dz > 0 {
            r := Vec3(
                x = 0
                y = dy
                z = dz
            )
            d = r.mag()
        } else {
            d = fmax(dx, fmax(dy, dz))
        }

        return (d, shader)
    }
}

///////////////////// transformations //////////////////////////

TranslatedSDF :: struct {
    sdf: SDF
    translation: Vec3
}

impl SDF for TranslatedSDF {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        return sdf.distance(v - translation)
    }
}

ScaledSDF :: struct {
    sdf: SDF
    scale: Vec3
}

impl SDF for ScaledSDF {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        return sdf.distance(v / scale)
    }
}

RotatedXSDF :: struct {
    sdf: SDF
    angle: double
}

RotatedYSDF :: struct {
    sdf: SDF
    angle: double
}

RotatedZSDF :: struct {
    sdf: SDF
    angle: double
}

impl SDF for RotatedXSDF {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        rad := angle / 360 * 2 * 3.1415
        c := cos(rad)
        s := sin(rad)
        y := c * v.y - s * v.z
        z := s * v.y + c * v.z
        return sdf.distance(Vec3(v.x, y, z))
    }
}

impl SDF for RotatedYSDF {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        rad := angle / 360 * 2 * 3.1415
        c := cos(rad)
        s := sin(rad)
        x := c * v.x + s * v.z
        z := -s * v.x + c * v.z
        return sdf.distance(Vec3(x, v.y, z))
    }
}

impl SDF for RotatedZSDF {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        rad := angle / 360 * 2 * 3.1415
        c := cos(rad)
        s := sin(rad)
        x := c * v.x - s * v.y
        y := s * v.x + c * v.y
        return sdf.distance(Vec3(x, y, v.z))
    }
}

////////////////////// combinations ///////////////////////
UnionSDF :: struct {
    sdf1: SDF
    sdf2: SDF
}

impl SDF for UnionSDF {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        a := sdf1.distance(v)
        b := sdf2.distance(v)
        if a.distance < b.distance {
            return a
        } else {
            return b
        }
    }
}

IntersectionSDF :: struct {
    sdf1: SDF
    sdf2: SDF
}

impl SDF for IntersectionSDF {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        a := sdf1.distance(v)
        b := sdf2.distance(v)
        if a.distance > b.distance {
            return a
        } else {
            return b
        }
    }
}

SubtractionSDF :: struct {
    sdf1: SDF
    sdf2: SDF
}

impl SDF for SubtractionSDF {
    fn distance(ref Self, v: Vec3) -> DistanceShaderPair {
        a := sdf1.distance(v)
        b := sdf2.distance(v)

        b.distance = -b.distance
        if a.distance > b.distance {
            return a
        } else {
            return b
        }
    }
}

////////////////////////////// shaders //////////////////////////////////

trait Shader {
    fn shade(ref Self, ray: Ray, raymarcher: ref Raymarcher) -> Vec3;
}

ConstantColorShader :: struct {
    color: Vec3
}

impl Shader for ConstantColorShader {
    fn shade(ref Self, ray: Ray, raymarcher: ref Raymarcher) -> Vec3 {
        return color
    }
}

//////
LambertianShader :: struct {
    color: Vec3
}

impl Shader for LambertianShader {
    fn shade(ref Self, ray: Ray, raymarcher: ref Raymarcher) -> Vec3 {
        (ld, lc) := raymarcher.sampleLightDirection()
        normal := raymarcher.normal(ray.origin)

        i := Vec3.dot(normal, ld)
        c := color * lc

        sdfHit := ray.origin + ld * 0.001
        shadowRay := Ray(sdfHit, ld)
        distance := raymarcher.sdf.distance(shadowRay.origin).distance

        while shadowRay.origin.mag() < raymarcher.backgroundDistance and fabs(distance) > 0.00005 {
            shadowRay.origin = shadowRay.origin + shadowRay.direction * distance
            distance = raymarcher.sdf.distance(shadowRay.origin).distance
        }

        shadow : double = 1.0
        if shadowRay.origin.mag() < raymarcher.backgroundDistance {
            shadow = 0.3
        }

        c *= fmax(i, 0.1)
        return c * shadow
    }
}

CheckerBoardShader :: struct {
    size: double
    shader1: Shader
    shader2: Shader
}


impl Shader for CheckerBoardShader {
    fn shade(ref Self, ray: Ray, raymarcher: ref Raymarcher) -> Vec3 {
        x := mod(ray.origin.x, 2 * size)
        y := mod(ray.origin.y, 2 * size)
        z := mod(ray.origin.z, 2 * size)

        if xor(x < size, xor(y < size, z < size)) {
            return shader1.shade(ray, raymarcher)
        } else {
            return shader2.shade(ray, raymarcher)
        }
    }
}

///////////////////////////// scene //////////////////////////////
Background :: struct {

}

impl Background {
    fn project(ref Self, ray: Ray) -> Vec3 {
        i := ray.direction.y + 0.5
        i *= i
        return Vec3(i, i, i)
    }
}

Raymarcher :: struct {
    sdf: SDF
    background: Background
    backgroundDistance: double = 1000
}

impl Raymarcher {
    fn project(ref Self, ray: Ray) -> Vec3 {
        ds := sdf.distance(ray.origin)

        while fabs(ds.distance) > 0.0000001 {
            ray.origin = ray.origin + ray.direction * ds.distance
            ds = sdf.distance(ray.origin)


            if ray.origin.mag() >= backgroundDistance {
                return background.project(ray)
            }
        }

        return ds.shader.shade(ray, self)
    }

    fn sampleLightDirection(ref Self) -> (ld: Vec3, lc: Vec3) {
        ld = Vec3(-1.0, 1.5, 1)
        ld.normalize()
        lc = Vec3(1, 1, 1)
    }

    fn normal(ref Self, p: Vec3) -> Vec3 {
        off : double = 0.001

        n := Vec3(
            x = sdf.distance(Vec3(p.x + off, p.y, p.z)).distance - sdf.distance(Vec3(p.x - off, p.y, p.z)).distance
            y = sdf.distance(Vec3(p.x, p.y + off, p.z)).distance - sdf.distance(Vec3(p.x, p.y - off, p.z)).distance
            z = sdf.distance(Vec3(p.x, p.y, p.z + off)).distance - sdf.distance(Vec3(p.x, p.y, p.z - off)).distance
        )
        return n.normalized()
    }
}


/////////////////////////////////////////////////


PerspectiveCamera :: struct {
    focalLength: double
    sensorWidth: double
    sensorHeight: double
}

impl PerspectiveCamera {
    fn project(ref Self, x: double, y: double) -> Ray {
        dir := Vec3(
            x = (x - 0.5) * sensorWidth
            y = -(y - 0.5) * sensorHeight
            z = -focalLength
        )
        return Ray(
            origin = Vec3(0, 0, 0)
            direction = dir.normalized()
        )
    }
}

Film :: struct {
    width: int
    height: int
    pixels: []Vec3

    antiAliasingSteps: int
}

impl Film {
    fn trigger(ref Self, camera: ref PerspectiveCamera, scene: ref Raymarcher) {

        printf("({}, {})`n", (width, height))

        fw := 1 / cast(double) width
        fh := 1 / cast(double) height

        printf("Rendering image with {} anti-aliasing step(s)`n", antiAliasingSteps)
        if antiAliasingSteps < 1 {
            antiAliasingSteps = 1
        }
        

        r := LinearCongruentialGenerator(
            cast get_time_microseconds()
        )

        for y : 0..height {
            if antiAliasingSteps >= 0 {
                row := y + 1
                printf("Rendering row {}/{}`n", (row, height))
            }

            for x : 0..width {
                fx := cast(double) x
                fy := cast(double) y

                color := Vec3(0, 0, 0)

                if antiAliasingSteps == 1 {
                    ray := camera.project((fx + 0.5) * fw, (fy + 0.5) * fh)
                    color= scene.project(ray)
                } else {
                    for i : 0..antiAliasingSteps {
                        ray := camera.project((fx + r.next_double()) * fw, (fy + r.next_double()) * fh)
                        c := scene.project(ray)
                        color += c
                    }
                    color = color * (1 / cast(double) antiAliasingSteps)
                }

                pixels[x + y * width] = color
            }
        }
    }
}

///////////////////////////////////////////////////////////////////////////

SDFAllocator :: struct {
    allocator: StackAllocator
}

impl SDFAllocator {
    fn create() -> Self {
        return SDFAllocator(StackAllocator.create(1024 * 2))
    }

    fn allocate(ref Self, $T: type) -> &T {
        t := cast(&T) allocator.allocate(1, @sizeof(T), @alignof(T))
        // printf("{}`n", (allocator))
        return t
    }

    fn sphere(ref Self, radius: double, shader: Shader) -> SDF {
        s := alloc(Sphere, allocator)
        s.radius = radius
        s.shader = shader
        return cast(SDF) ref <<s
    }

    fn sphereT(ref Self, position: Vec3, radius: double, shader: Shader) -> SDF {
        return self.translate(self.sphere(radius, shader), position)
    }

    fn planeY(ref Self, location: double, shader: Shader) -> SDF {
        s := alloc(PlaneY, allocator)
        s.location = location
        s.shader = shader
        return cast(SDF) ref <<s
    }

    fn cube(ref Self, radius: double, shader: Shader) -> SDF {
        s := alloc(Cube, allocator)
        s.radius = radius
        s.shader = shader
        return cast(SDF) ref <<s
    }

    fn cubeT(ref Self, position: Vec3, radius: double, shader: Shader) -> SDF {
        return self.translate(self.cube(radius, shader), position)
    }

    fn translate(ref Self, sdf: SDF, translation: Vec3) -> SDF {
        t := alloc(TranslatedSDF, allocator)
        t.sdf = sdf
        t.translation = translation
        return cast(SDF) ref <<t
    }

    fn rotateX(ref Self, sdf: SDF, angle: double) -> SDF {
        t := alloc(RotatedXSDF, allocator)
        t.sdf = sdf
        t.angle = angle
        return cast(SDF) ref <<t
    }

    fn rotateY(ref Self, sdf: SDF, angle: double) -> SDF {
        t := alloc(RotatedYSDF, allocator)
        t.sdf = sdf
        t.angle = angle
        return cast(SDF) ref <<t
    }

    fn rotateZ(ref Self, sdf: SDF, angle: double) -> SDF {
        t := alloc(RotatedZSDF, allocator)
        t.sdf = sdf
        t.angle = angle
        return cast(SDF) ref <<t
    }

    fn scale(ref Self, sdf: SDF, x: double = 1, y: double = 1, z: double = 1) -> SDF {
        t := alloc(ScaledSDF, allocator)
        t.sdf = sdf
        t.scale = Vec3(x, y, z)
        return cast(SDF) ref <<t
    }

    fn add(ref Self, sdf1: SDF, sdf2: SDF) -> SDF {
        t := alloc(UnionSDF, allocator)
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return cast(SDF) ref <<t
    }

    fn intersect(ref Self, sdf1: SDF, sdf2: SDF) -> SDF {
        t := alloc(IntersectionSDF, allocator)
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return cast(SDF) ref <<t
    }

    fn subtract(ref Self, sdf1: SDF, sdf2: SDF) -> SDF {
        t := alloc(SubtractionSDF, allocator)
        t.sdf1 = sdf1
        t.sdf2 = sdf2
        return cast(SDF) ref <<t
    }
}

fn renderImage(film: ref Film)
{
    for i : 0..film.pixels.length {
        film.pixels[i] = Vec3(0, 0, 0)
    }

    as := (cast(double) film.width) / (cast(double) film.height)
    camera := PerspectiveCamera(
        focalLength = 50
        sensorWidth = as * 24
        sensorHeight = 24
    )

    bg := Background()

    all := SDFAllocator.create()

    //
    sChecker1    := LambertianShader(Vec3(0.9, 0.8, 0.8))
    sChecker2    := LambertianShader(Vec3(0.2, 0.1, 0.1))
    sChecker  := CheckerBoardShader(1, sChecker1, sChecker2)

    sWhite    := LambertianShader(Vec3(1, 1, 1))
    sBlack    := LambertianShader(Vec3(0, 0, 0))
    sRed    := LambertianShader(Vec3(1, 0, 0))
    sGreen  := LambertianShader(Vec3(1, 1, 0))
    sBlue   := LambertianShader(Vec3(0, 0, 1))

    //
    ground := all.planeY(-1.25, sWhite)
    s1 := all.sphereT(Vec3(0, 0.5, 0), 1, sRed)
    s2 := all.sphereT(Vec3(0, 0, 0), 0.3, sGreen)
    c1 := all.cubeT(Vec3(0, 0.5, 0), 0.8, sBlue)

    sdf := {
        sdf := {
            sdf: SDF
            {
                sdf = c1
                sdf = all.subtract(sdf, s1)
                sdf = all.add(sdf, s2)

                sdf = all.rotateX(sdf, -20.0)
                sdf = all.rotateY(sdf, 20)
            }
            
            // cube
            // {
            //     sdf = s1
            //     sdf = all.subtract(sdf, all.sphereT(Vec3(-1, 0, 1), 1.2, sWhite))
            // }

            all.add(sdf, ground)
        }
        sdf
    }
    
    finalSDF := all.translate(sdf, Vec3(0, 0, -7.5))

    raymarcher := Raymarcher(
        sdf = finalSDF
        background = bg
        backgroundDistance = 15
    )

    film.trigger(camera, raymarcher)
}

fn Main()
{
    //
    scale : int = 2
    width : int = 192 * scale
    height : int = 128 * scale
    antiAliasingSteps : int = 1

    imageArr := Array[Vec3].create()

    imageArr.resize(width * height)

    film := Film(width, height, imageArr.slice(), antiAliasingSteps)

    start := get_time_milliseconds()
    renderImage(film)
    end := get_time_milliseconds()
    dur := (cast(double) end - start) / 1000.0

    printf("Rendering took {} seconds`n", dur)

    // store image as bmp
    write_bmp(cast(i32) width, cast(i32) height, cast(&double) imageArr.get_raw(), "sdfr.bmp")
}
