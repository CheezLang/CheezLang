C :: import std.c
use import std.util
use import std.alloca

Foo :: struct #copy {
    num : int
    bar : Bar
    baz : Baz
}

Bar :: struct #copy {
    str : string
    boo : bool
}

Baz :: enum #copy {
    A           = 353
    B : int     = 46
    C : ^int    = 96
}

do_print_any :: (data: ^void, typ: ^TypeInfo) {
    match typ.kind {
        use TypeInfoKind

        Int($tint) -> {
            if tint.signed {
                ptr := cast(^int) data
                C.printf("%lld", <<ptr)
            } else {
                ptr := cast(^uint) data
                C.printf("%llu", <<ptr)
            }
        }

        Float -> {
            if typ.size == 4 {
                ptr := cast(^float) data
                C.printf("%g", double(<<ptr))
            } else if typ.size == 8 {
                ptr := cast(^double) data
                C.printf("%g", <<ptr)
            } else {
                C.printf("<float: invalid size %lld>", typ.size)
            }
        }

        Bool -> {
            ptr := cast(^bool) data
            if <<ptr then C.printf("true") else C.printf("false")
        }

        Char -> {
            ptr := cast(^char) data
            C.printf("'%c'", <<ptr)
        }

        Pointer($target) -> {
            ptr := cast(^^void) data
            C.printf("0x%llX", <<ptr)
        }

        Slice($target) -> {
            ptr := <<cast(^[]void) data
            C.printf("[")
            for i in 0..ptr.length {
                if i > 0 then C.printf(", ")

                elem := pointer_add(ptr.data, i * target.size)
                do_print_any(elem, target)
            }
            C.printf("]")
        }

        String -> {
            str := <<cast(^string) data
            C.printf("%.*s", i32(str.bytes.length), str.bytes.data)
        }

        Struct($tstruct) -> {
            ptr := cast(^void) data
            C.printf("%.*s", i32(tstruct.name.bytes.length), tstruct.name.bytes.data)

            C.printf("(")
            if tstruct.members.data != null {
                for i in 0..tstruct.members.length {
                    if i > 0 then C.printf(", ")
                    member_info := ^tstruct.members[i]
                    member_ptr  := pointer_add(ptr, member_info.offset)
                    member_type := member_info.typ
                    C.printf("%.*s = ", i32(member_info.name.bytes.length), member_info.name.bytes.data)
                    do_print_any(member_ptr, member_type)
                }
            }
            C.printf(")")
        }

        TypeInfoKind.Enum($tenum) -> {
            ptr := cast(^void) data
            C.printf("%.*s.", i32(tenum.name.bytes.length), tenum.name.bytes.data)

            tag := match tenum.tag_type.size {
                1  -> int(<<cast(^u8) ptr)
                2  -> int(<<cast(^u16) ptr)
                4  -> int(<<cast(^u32) ptr)
                8  -> int(<<cast(^u64) ptr)
                $_ -> {
                    @panic("Unsupported tag size")
                    0
                }
            }

            for(by_ref=true) mem in tenum.members {
                if mem.tag == tag {
                    C.printf("%.*s", i32(mem.name.bytes.length), mem.name.bytes.data)

                    if mem.typ != null {
                        mem_val_ptr := pointer_add(ptr, tenum.tag_type.size)

                        C.printf("(")
                        do_print_any(mem_val_ptr, mem.typ)
                        C.printf(")")
                    }
                }
            }
        }

        $_ -> {
            C.printf("<unknown>")
        }
    }
}

print_any :: (data: any) {
    do_print_any(data.val, data.typ)
}

println_any :: (data: any) {
    do_print_any(data.val, data.typ)
    C.puts("")
}

Main :: () {
    bar := Bar("lol", true)
    foo := Foo(12345, bar, Baz.A)

    a := 123
    b := true
    s : []int = [a, 353, 987]
    w := "lol"

    println_any(a)
    println_any(b)
    println_any(s)
    println_any(w)

    println_any(bar)
    println_any(foo)
    println_any(Foo(654, bar, Baz.B(420)))
    println_any(Foo(741, bar, Baz.C(^a)))

    println_any((struct{
        num       : int
        word      : string
        boolean   : bool
        floater   : double
        character : char
    })(1, "hi", false, 5.3, 'x'))
}