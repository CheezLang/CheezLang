// #load("examples/trait_function_call_performance")
// #load("examples/graphyfier/graphyfier")
// #load("examples/pathtracer2/main")
// #load("examples/geometry_draw_thingy")
// #load("examples/signed_distance_field_renderer")
// #load("compiler/main")
// #load("examples/snake/snake")
// #load("examples/pathtracer")
// #load("compiler/compiler_test")

// #load("tests/language/match3")
// #load("tests/library/arena_allocator1")
// #load("tests/other/test1")
// #load("examples/project_euler/005_smallest_multiple")

//*

#load("std:c")
#load("std:util")

Foo :: struct #copy {
    num : int
    bar : Bar
    // baz : Baz
}

Bar :: struct #copy {
    str : string
    boo : bool
}

Baz :: enum #copy {
    A
    B : int
    C : &int
}

print :: (data: $T) {
    typ := @type_info(T)
    print_any(&data, typ)
}

println :: (data: $T) {
    typ := @type_info(T)
    print_any(&data, typ)
    c_puts("")
}

print_any :: (data: &any, typ: &TypeInfo) {
    match typ.kind {
        TypeInfoKind.Int($tint) -> {
            if tint.signed {
                ptr := cast(&int) data
                c_printf("%lld", <<ptr)
            } else {
                ptr := cast(&uint) data
                c_printf("%llu", <<ptr)
            }
        }

        TypeInfoKind.Float -> {
            if typ.size == 4 {
                ptr := cast(&float) data
                c_printf("%f", double(<<ptr))
            } else if typ.size == 8 {
                ptr := cast(&double) data
                c_printf("%f", <<ptr)
            } else {
                c_printf("<float: invalid size %lld>", typ.size)
            }
        }

        TypeInfoKind.Bool -> {
            ptr := cast(&bool) data
            if <<ptr then c_printf("true") else c_printf("false")
        }

        TypeInfoKind.Char -> {
            ptr := cast(&char) data
            ch  := <<ptr
            c_printf("'%c'", <<ptr)
        }

        TypeInfoKind.Slice($target) -> {
            if int(target.kind) == int(TypeInfoKind.Char) {
                if data == null {
                    c_printf("null")
                } else {
                    ptr := <<cast(&[]char) data
                    c_printf("`"%.*s`"", ptr.length, ptr.data)
                }
            } else {
                ptr := <<cast(&[]any) data
                c_printf("[")
                for i : 0..ptr.length {
                    if i > 0 then c_printf(", ")

                    elem := pointer_add(ptr.data, i * target.size)
                    print_any(elem, target)
                }
                c_printf("]")
            }
        }

        TypeInfoKind.Struct($tstruct) -> {
            ptr := cast(&any) data
            c_printf("%.*s", i32(tstruct.name.length), tstruct.name.data)

            c_printf("(")
            if tstruct.members.data != null {
                for i : 0..tstruct.members.length {
                    if i > 0 then c_printf(", ")
                    member_info := &tstruct.members[i]
                    member_ptr  := pointer_add(ptr, member_info.offset)
                    member_type := member_info.typ
                    print_any(member_ptr, member_type)
                }
            }
            c_printf(")")
        }

        $_ -> {
            c_printf("<unknown>")
        }
    }
}

Main :: () {
    bar := Bar("lol", true)
    foo := Foo(12345, bar)

    tchar      := @type_info(char)
    tint       := @type_info(int)
    tbool      := @type_info(bool)

    tstring    := @type_info(string)
    tint_slice := @type_info([]int)

    tBar       := @type_info(Bar)
    tFoo       := @type_info(Foo)

    a := 123
    b := true
    s : []int = [a, 353, 987]
    w := "lol"

    println(a)
    println(b)
    println(s)
    println(w)

    println(bar)
    println(foo)

    println((struct{
        num : int
        word : string
        boolean : bool
        floater : double
        character : char
    })(1, "hi", false, 5.3, 'x'))

    // println(<<tBar)
}
// */